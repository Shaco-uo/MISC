[DEFNAME tags_dialog]

// tags_dialog_background		3500
// tags_dialog_text_box 		87
// // buttons id
// tags_dialog_set				4005 4007
// tags_dialog_remove			4017 4019
// tags_dialog_radio			2104 9668
// tags_dialog_go_small		5603 5607
// tags_dialog_go_large		5601 5605


tags_dialog_background		9350
tags_dialog_text_box 		2604
// buttons id
tags_dialog_set				4005 4007
tags_dialog_remove			4017 4019
tags_dialog_radio			9026 9027
tags_dialog_go_small		5603 5607
tags_dialog_go_large		5601 5605

[PLEVEL 4]
tags
gevents
// ***************************
[FUNCTION gevents]
targetf f_events

[FUNCTION f_events]
f_get_eventlist <argo>

[FUNCTION tags]
targetf f_tags

[FUNCTION f_tags]
f_get_taglist <argo>

[FUNCTION f_get_taglist]
if !<args>
	ref1=<src>
else
	ref1=<args>
endif
if <ref1.tagcount>
	src.ctag.taglist=<ref1>
	src.dialog d_taglist
else
	src.sysmessage @,,1 <ref1.name> has no tags.
	return 1
endif

[FUNCTION f_get_eventlist]
if !<args>
	ref1=<src>
else
	ref1=<args>
endif
if <ref1.events> || <ref1.tevents>
	src.ctag.eventlist=<ref1>
	src.dialog d_event_list
else
	src.sysmessage @,,1 <ref1.name> has no events.
	return 1
endif



[DIALOG d_taglist]
400,200
PAGE 0
local.x=60
local.y=60
ref1=<src.ctag.taglist>
local.q=<eval <ref1.tagcount>*20>+60
if <isempty <src.ctag.large_dialog>>
	resizepic <eval <local.x>-29> <eval <local.y>-15> <def.tags_dialog_background> 260 <local.q> // background
	button <eval <local.x>+210> <eval <local.y>-10> <def.tags_dialog_go_large> 1 0 3 // go large
else
	resizepic <eval <local.x>-29> <eval <local.y>-15> <def.tags_dialog_background> 400 <local.q> // background
	button <eval <local.x>+350> <eval <local.y>-10> <def.tags_dialog_go_small> 1 0 3 // go small
endif
for t 0 <eval <ref1.tagcount>-1>
	dhtmlgump <local.x> <local.y> 340 20 0 0 <ref1.tagat.<local.t>.key> = <ref1.tagat.<local.t>.val>
	radio <eval <local.x>-21> <eval <local.y>-2> <def.tags_dialog_radio> 0 <eval <local.t>+1>
	local.y += 20
endfor
local.y += 5
if <isempty <src.ctag.large_dialog>>
	gumppictiled <eval <local.x>+55> <eval <local.y>-1> 142 21 <def.tags_dialog_text_box> 	// text background small
else
	gumppictiled <eval <local.x>+55> <eval <local.y>-1> 282 21 <def.tags_dialog_text_box> 	// text background large
endif
dtextentrylimited  <eval <local.x>+58> <local.y> 122 15 0480 0 20 0
button <eval <local.x>+22> <local.y> <def.tags_dialog_set> 1 0 1	// set
button <eval <local.x>-15> <local.y> <def.tags_dialog_remove> 1 0 2	// remove

[DIALOG d_taglist button]
ON=0
	src.ctag.taglist=
	src.ctag.large_dialog=
	return 1
ON=1 2
	ref1=<src.ctag.taglist>
	if !<argchk>
		src.sysmessage @33,,1 NO ARGCHK.
		src.f_get_taglist <ref1>
		return 1
	endif
	if <argn1>==1
		src.sysmessage @,,1 <ref1.name> TAG <ref1.tagat.<eval <argchkid>-1>.key> has been set to <argtxt[0]>.
		ref1.tag.<ref1.tagat.<eval <argchkid>-1>.key> = <argtxt[0]>
	else
		src.sysmessage @,,1 <ref1.name> TAG <ref1.tagat.<eval <argchkid>-1>.key> has been removed.
		ref1.tag.<ref1.tagat.<eval <argchkid>-1>.key> = 
	endif
	src.f_get_taglist <ref1>
	return 1
on=3
	ref1=<src.ctag.taglist>
	if <isempty <src.ctag.large_dialog>>
		src.ctag.large_dialog=1
	else
		src.ctag.large_dialog=
	endif
	src.f_get_taglist <ref1>
	return 1
	
[DIALOG d_event_list]
400,200
PAGE 0
local.x=60
local.y=60
ref1=<src.ctag.eventlist>
if <ref1.events>
	args=<ref1.events>
	local.q=<eval <GetArgCount <args>>*20>+60
	resizepic <eval <local.x>-29> <eval <local.y>-15> <def.tags_dialog_background> 200 <local.q> // background
	for 0 <eval <GetArgCount <args>>-1>
		dhtmlgump <eval <local.x>+10> <local.y> 340 20 0 0 <argv[<local._for>]>
		button <eval <local.x>-19> <eval <local.y>+1> <def.tags_dialog_remove> 1 0 <eval <local._for>+1>
		local.y += 20
	endfor
endif
if <ref1.tevents>
	args=<ref1.tevents>
	if !<ref1.events>
		local.q=<eval <GetArgCount <args>>*20>+60
		resizepic <eval <local.x>-29> <eval <local.y>-15> <def.tags_dialog_background> 200 <local.q> // background
	endif
	for 0 <eval <GetArgCount <args>>-1>
		dhtmlgump <eval <local.x>+10> <local.y> 340 20 0 0 <def.bfont_brown><argv[<local._for>]>
		local.y += 20
	endfor
endif

[DIALOG d_event_list button]
ON=0
	src.ctag.eventlist=
	return 1
ON=1 100
	ref1=<src.ctag.eventlist>
	local.a=<eval <argn1>-1>
	args=<ref1.events>
	ref1.events -<argv[<local.a>]>
	src.sysmessage @,,1 <ref1.name> event <argv[<local.a>]> has been removed.
	src.f_get_eventlist <ref1>
	return 1

[EOF]