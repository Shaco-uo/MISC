[EVENTS e_npc_generic_event]
//Generic event to all npc
ON=@SpellInterrupt
	ARGN2=1000

ON=@HitTRY	// HIT CHANCE
	actdiff = 1000 //actdiff  above or equal 0 always hit
	if <argo.isvalid> && <argo.isweapon>
		local.skill = <<serv.skill.<argo.skill>.key>>
	endif
	if <action> == skill_wrestling
		local.skill = <wrestling>
	endif
	// local.skill += 50 // increase here for more hit chance
	if rand(1000) >= <dlocal.skill>
		actdiff = -1000 //actdiff < 0 always miss
	endif

ON=@Hit
	LOCAL.ItemDamageChance = 0
	if <owner> && <owner.isplayer>	// pet damage by food %
		local.foodPercent = <MulDiv <food>,100,<maxfood>>
		local.damtotal = <MulDiv <argn1>,<local.foodPercent>,100>
		// serv.log dam <argn1> food <local.foodPercent>% total <local.damtotal>
		argn1 = <local.damtotal>
	endif
	
ON=@GETHIT
	LOCAL.ItemDamageChance = 0
	// PET DMG TO OWNER
	if <npc> && <owner>        //must add pets damage to the pet's owner for rewards at the end
		ref1=<owner>
		if <isvalid>
			if <attacker.id <ref1>> <= -1
				attacker.add <ref1>
			endif
			attacker.<attacker.id <ref1>>.damage=<eval (<attacker.<attacker.id <ref1>>.damage>+<argn1>)>
			//say <ref1.name>'s damage:  <attacker.<attacker.id <ref1>>.damage>
		endif
	endif

ON=@Hitparry
	Local.ItemParryDamageChance = 0  //chance of the shield taking damage i.e if this was 50 then its 50% chance

ON=@RegenStat	// PET HUNGER DESERT
	if <local.statid>==3 && <owner> && <food>==0 && !(<flags> & statf_conjured) // && !<isevent.e_custom_vendor>	 
		// serv.log stat <local.statid>, food (<local.value>) is now <food>
		timerf 1,emote appears to have decided that it is better off without a master
		release
	endif

[eof]