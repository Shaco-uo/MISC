[DEFNAME SLAYER_SYSTEM]
slayer_hit_chance           33      // 33%
slayer_hit_fx               3,i_fx_swirly_sparks,10,20,10
slayer_hit_snd              486
// Buffs
slayer_icon_1               1027
slayer_icon_2               1021    // check icon in sphere_defs.scp
slayer_icon_3               1022    // check icon in sphere_defs.scp
slayer_cliloc_1             1065741 // Slayer Weapon - <def.slayer_cliloc_undead>           
slayer_cliloc_undead        1079797 // src.addbuff 1027,<def.slayer_cliloc_1>,1079797,0	// Slayer Weapon - Undead
slayer_cliloc_demon         1065743 // Slayer Weapon - Demon
slayer_cliloc_dragon        1065744
slayer_cliloc_beast         1065745
slayer_cliloc_elemental     1065746
slayer_cliloc_repond        1065747
slayer_cliloc_giant         1065748
slayer_cliloc_orc           1065749
slayer_cliloc_arachnid      1065750
slayer_cliloc_Mage          1065751
slayer_cliloc_Reptile       1065752
slayer_cliloc_Fey           1065753
slayer_cliloc_Bovine        1065754

[FUNCTION f_setbonus_slayer] // usage on @create: f_setbonus_slayer Dragon,15
if <isempty <tag.slayer1>>
    local.t=1
    events=+t_slayer_weapon
else
    if <isempty <tag.slayer2>>
        local.t=2
    else
        if <isempty <tag.slayer3>>
            local.t=3
        else
            serv.log <baseid> <name> <uid> has more than 3 slayer bonus.
            return 1
        endif
    endif
endif
tag.slayer<local.t>=<argv[0]>
tag.slayer_dam<local.t>=<argv[1]>
local.info=+<argv[1]> <argv[0]> Slayer
f_set_info <local.info>

[FUNCTION f_setbonus_slayer_rand] // usage on @create: f_setbonus_slayer_rand 15
f_setbonus_slayer <def.slayer_<R1,13>>,<args>

[FUNCTION f_setbonus_slayer_elite]
tag.elite_dam=<args>
events=+t_elite_slayer
local.info=+<eval <tag.elite_dam>> Elite Slayer
f_set_info <local.info>

[FUNCTION f_setbonus_slayer_boss]
tag.boss_dam=<args>
events=+t_boss_slayer
local.info=+<eval <tag.boss_dam>> Boss Slayer
f_set_info <local.info>

[FUNCTION f_setbonus_slayer_invasion]
tag.invasion_dam=<args>
events=+t_invasion_slayer
local.info=+<eval <tag.invasion_dam>> Invasion Damage
f_set_info <local.info>

// *************************************
[EVENTS e_slayer_weapon]    // check how to add spellhit instead of using spelldef
ON=@Hit
	if !<isempty <src.tag.SlayerGroup>>
        if strmatch(*<src.tag.SlayerGroup>*,<argo.tag.slayer1>) 
			local.slayer=1
		endif
        if strmatch(*<src.tag.SlayerGroup>*,<argo.tag.slayer2>)
			local.slayer=2
		endif
        if strmatch(*<src.tag.SlayerGroup>*,<argo.tag.slayer3>)
			local.slayer=3
		endif
        if <local.slayer>   // && rand(100) <= <def.slayer_hit_chance>
            // say hit <argo.tag.slayer<local.slayer>> <argo.tag0.slayer_dam<local.slayer>> // debug
    		argn1 += <argo.tag0.slayer_dam<local.slayer>>
			src.effect=<def.slayer_hit_fx>,<argo.color>
			src.sound=<def.slayer_hit_snd>
        endif
	endif

ON=@SpellSuccess
    if <serv.spell.<argn1>.flags>&spellflag_harm && <act.isvalid>
        ref1=<act>
        ref2=<src.weapon>
        if !<isempty <ref1.tag.slayergroup>>
            if strmatch(*<src.tag.slayergroup>*,<ref2.tag.slayer1>) 
                local.slayer=1
            endif
            if strmatch(*<src.tag.slayergroup>*,<ref2.tag.slayer2>)
                local.slayer=2
            endif
            if strmatch(*<src.tag.slayergroup>*,<ref2.tag.slayer3>)
                local.slayer=3
            endif
            if <local.slayer>   // && rand(100) <= <def.slayer_hit_chance>
                // sayu <ref2.tag.slayer<local.slayer>> bonus <ref2.tag0.slayer_dam<local.slayer>> color <def.color_<ref2.tag.slayer<local.slayer>>>
                local.effectcolor=<ref2.color>
                ref1.effect=<def.slayer_hit_fx>,<ref2.color>
                ref1.sound=<def.slayer_hit_snd>
                argn2 += <eval <ref2.tag0.slayer_dam<local.slayer>>*10>
            endif
        endif
    endif

[EVENTS e_elite_slayer]
ON=@Hit
    if <src.tag0.elite>==1
        src.effect=<def.slayer_hit_fx>,<argo.color>
        src.sound=<def.slayer_hit_snd>
        argn1 += <argo.tag0.elite_dam>
    endif

ON=@SpellSuccess
    if <serv.spell.<argn1>.flags>&spellflag_harm && <act.isvalid> && <act.tag0.elite>==1
        ref1=<act>
        ref2=<src.weapon>
        ref1.effect=<def.slayer_hit_fx>,<ref2.color>
        ref1.sound=<def.slayer_hit_snd>
        argn2 += <eval <argo.tag0.elite_dam>*10>
    endif

[EVENTS e_boss_slayer]
ON=@Hit
    if <src.tag0.elite>==2
        src.effect=<def.slayer_hit_fx>,<argo.color>
        src.sound=<def.slayer_hit_snd>
        argn1 += <argo.tag0.boss_dam>
    endif

ON=@SpellSuccess
    if <serv.spell.<argn1>.flags>&spellflag_harm && <act.isvalid> && <act.tag0.elite>==2
        ref1=<act>
        ref2=<src.weapon>
        ref1.effect=<def.slayer_hit_fx>,<ref2.color>
        ref1.sound=<def.slayer_hit_snd>
        argn2 += <eval <argo.tag0.boss_dam>*10>
    endif

[EVENTS e_invasion_slayer]
ON=@Hit
	if <src.tag0.invasion>==1
        src.effect=<def.slayer_hit_fx>,<argo.color>
        src.sound=<def.slayer_hit_snd>
        argn1 += <argo.tag0.invasion_dam>
    endif

ON=@SpellSuccess
    if <serv.spell.<argn1>.flags>&spellflag_harm && <act.isvalid> && <act.tag0.invasion_dam>==1
        ref1=<act>
        ref2=<src.weapon>
        ref1.effect=<def.slayer_hit_fx>,<ref2.color>
        ref1.sound=<def.slayer_hit_snd>
        argn2 += <eval <argo.tag0.invasion_dam>*10>
    endif

// ********************************************
[TYPEDEF t_slayer_weapon]
// ON=@Clienttooltip    // added at main tooltip script
ON=@Equip
	src.events +e_slayer_weapon
    // src.tag.slayer<tag.slayer1> += <tag.slayer1>
	if <src.isplayer>
        if !<isempty <tag.slayer1>>
            src.addbuff <def.slayer_icon_1>,<def.slayer_cliloc_1>,<def.slayer_cliloc_<tag.slayer1>>,0	// src.addbuff 1027,1065741,1079797,0	// Slayer Weapon - Undead
        endif
        if !<isempty <tag.slayer2>>
            src.addbuff <def.slayer_icon_2>,<def.slayer_cliloc_1>,<def.slayer_cliloc_<tag.slayer2>>,0  // create cliloc 2
        endif
        if !<isempty <tag.slayer3>>
            src.addbuff <def.slayer_icon_3>,<def.slayer_cliloc_1>,<def.slayer_cliloc_<tag.slayer3>>,0  // create cliloc 3
        endif
    endif
ON=@Unequip 
	src.events -e_slayer_weapon
	if <src.isplayer>
        if !<isempty <tag.slayer1>>
            src.removebuff <def.slayer_icon_1>
        endif
        if !<isempty <tag.slayer2>>
            src.removebuff <def.slayer_icon_2>
        endif
        if !<isempty <tag.slayer3>>
            src.removebuff <def.slayer_icon_3>
        endif
    endif

[TYPEDEF t_elite_slayer]
ON=@Equip 
	src.events +e_elite_slayer
	if <src.isplayer>
		src.addbuff <def.slayer_icon_2>,<def.slayer_cliloc_1>,1065754,0	// Slayer Weapon - Elite
    endif
ON=@Unequip 
	src.events -e_elite_slayer
	if <src.isplayer>
		src.removebuff <def.slayer_icon_2>
	endif

[TYPEDEF t_boss_slayer]
ON=@Equip 
	src.events +e_boss_slayer
	if <src.isplayer>
		src.addbuff <def.slayer_icon_3>,<def.slayer_cliloc_1>,1065755,0	// Slayer Weapon - Boss
    endif
ON=@Unequip 
	src.events -e_boss_slayer
	if <src.isplayer>
		src.removebuff <def.slayer_icon_3>
	endif

[TYPEDEF t_invasion_slayer]
ON=@Equip
    src.sound=605
	src.events=+e_invasion_slayer
	if <src.isplayer>
		src.addbuff <def.slayer_icon_3>,1065724,1065725,0,15
    endif
ON=@Unequip
	src.events=-e_invasion_slayer
	if <src.isplayer>
		src.removebuff <def.slayer_icon_3>
	endif

[EOF]