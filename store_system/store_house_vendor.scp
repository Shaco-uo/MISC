[ITEMDEF i_house_store_sign]
DEFNAME=i_house_store_sign
ID=01e5e
NAME=House Store
TYPE=t_container
TDATA2=042
WEIGHT=10.0
CATEGORY=Custom
SUBSECTION=Store System
DESCRIPTION=House Store
ON=@Create
	attr=010
	color=03b2
	timerf 1,f_set_house_store

ON=@Click
	return 1

ON=@ClientTooltip
	ADDCLILOC 1042971,<def.bfont_size7><def.bfont_cyan><name><def.bfont_white><def.bfont_size4>
    if !<isempty <src.account.tag.house_store>> && <src.account.tag.house_store>==<uid>	// is owner
        if <tag0.gold>!=0
            ADDCLILOC 1060658,Gold Earned,<def.bfont_lyellow><f_numformat <tag0.gold>><def.bfont_white>
        endif
    else
        ref2=<tag.store_owner>
        ADDCLILOC 1060659,Owner,<def.bfont_lyellow><ref2.name><def.bfont_white>
    endif
    if <f_house_store_total_items <uid>> > 0
        ADDCLILOC 1060660,Items,<def.bfont_lyellow><f_house_store_total_items <uid>><def.bfont_white>
    endif
	return 1

ON=@ContextMenuRequest
    if !<isempty <src.account.tag.house_store>> && <src.account.tag.house_store>==<uid>
        src.addcontextentry 666,1150328,020,055	// OWNER MENU
    endif
	if <src.isgm>
		src.addcontextentry 664,3002132,020,055	// Configuration (GM Menu)
	endif

ON=@ContextMenuSelect
	if <distance> > 3
		src.sysmessage @,,1 <name> is too far away.
		return 1
	endif
	if <argn>==664 && <src.isgm>	// GM Menu
		src.dialogclose d_house_store_gm
		dialog d_house_store_gm
		return 1
	endif
	if <argn>==666 					// Owner Menu
		ref1=<src.account.tag.house_store>
		src.dialogclose d_house_store_owner
		ref1.dialog d_house_store_owner
		return 1
	endif

ON=@Destroy
    serv.f_remove_all_vendors <uid>
    serv.f_recycle_store <uid>,<tag.store_owner>,<tag.recycle_pack>
    return 1

[ITEMDEF i_deed_vendor_house]
DEFNAME=i_deed_vendor_house
ID=i_deed
NAME=House Vendor
TYPE=t_script
VALUE=500000
tag.itemlevel=1
CATEGORY=Custom
SUBSECTION=Store System
DESCRIPTION=House Vendor deed
ON=@Dclick
    if <topobj>!=<src> || <isinbank>
        src.sysmessage @,,1 <name> must be in your backpack.
        return 1
    endif
	if !<src.isonhouse>
		src.sysmessage @,,1 You must be in a house to use <name>.
		return 1
	endif
    if !<isempty <src.account.tag.house_store>> 
        src.sysmessage @,,1 You already have a house store.
        return 1
    endif
	ref1=<src.region.uid>
    if !<ref1.isowner <src>> && !<src.isgm>
        src.sysmessage @,,1 You must be in your house to use <name>.
        return 1
    endif
    // if (<def.store_house_vendorspublic>) && !(<uid.<ref1.more2>.tag0.public>)    // update this
    //     src.sysmessage @,,1 You cannot place this vendor or barkeep.  Make sure the house is public and has sufficient storage available.
    //     return 1
    // endif
    serv.newitem=i_house_store_sign
    new.p=<src.p>
    serv.f_house_store_set_owner <new>,<ref1.owner>
    remove
    return 1


[FUNCTION f_set_house_store]
region.events +r_store_system
region.tag.store_sign=<uid>
tag.max_vendors=<def.store_house_vendor_max>
tag.max_skins=<def.store_skin_amt>
tag.skins_used=3
tag.skin_1=c_store_vendor_m,Human Male,colors_skin,"Can equip items"
tag.skin_2=c_store_vendor_f,Human Female,colors_skin,"Can equip items"
tag.skin_3=c_slime,Frost Slime,0481,"Cold & Slippery"

serv.newitem=i_backpack,1,<uid>   			// when vendor human is deleted his gear goes here
tag.recycle_pack=<new>
new.contp=20,100
new.attr |= attr_move_never

local.x=60	
for 1 <def.store_house_vendor_max>          // create vendor storage packs
serv.newitem=i_backpack,1,<uid>				// store vendor packs in sign
tag.vendor_pack_<local._for>=<new>			// store vendor pack uid on sign
new.contp=<local.x>,100						// vendor pack location in layer store
new.attr |= attr_move_never
new.color=<local.x>	                		// any color will do
new.tag.skin_current=<R1,2>	        		// 1st use, human vendor (m/f)
local.x += 20
endfor
dispid=0bd3
nudgeup 3
sayu <name> Set!

[FUNCTION f_house_store_set_owner]  // f_house_store_set_owner <sign_uid>,<player_uid>
ref1=<argv[0]>	// store_sign
ref2=<argv[1]>  // player
ref2.account.tag.house_store=<ref1>
ref2.sysmessage @52,,1 You set up a store in your home.
ref1.tag.store_owner=<ref2>
ref1.tag.builton=<serv.rtime>
ref1.color=0
ref1.resendtooltip 1,0

[FUNCTION f_house_store_total_items]
ref1=<args>	// store sign
for 1 <def.store_house_vendor_max>
if !<isempty <ref1.tag.vendor_pack_<local._for>>>
	ref2=<tag.vendor_pack_<local._for>>
	local.total += <ref2.rescount>
endif
endfor
return <local.total>

[FUNCTION f_house_store_signmove]
ref1=<args>	// sign uid
ref2=<ref1.region.uid>	// sign
serv.b <ref2.name>
if <argo>
	sysmessage @,,1 Invalid Target.
	return 1
endif
if <targp>
	local.old_p=<ref1.p>
	ref1.p <targp>
	if !<ref1.region.uid> || <ref1.region.uid>!=<ref2>
		sysmessage @,,1 Invalid Target.
		ref1.p <local.old_p>
	endif
endif
ref1.dialog d_store_sign
return 1

[EOF]