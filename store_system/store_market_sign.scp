[ITEMDEF i_store_sign]
DEFNAME=i_store_sign
ID=01e5e
NAME=Market Store
TYPE=t_container
TDATA2=042
WEIGHT=10.0
CATEGORY=Custom
SUBSECTION=Store System
DESCRIPTION=Market Store
ON=@Create
	attr=010
	color=03b2
	timerf 1,f_set_store

ON=@Click
	return 1

ON=@ClientTooltip
	ADDCLILOC 1042971,<def.bfont_size7><def.bfont_cyan><name><def.bfont_white><def.bfont_size4>
	if !<isempty <tag.store_owner>>
		ref2=<tag.store_owner>
		if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<uid>	// is owner
			if <tag0.gold>!=0
			ADDCLILOC 1060658,Gold Earned,<def.bfont_lyellow><f_numformat <tag0.gold>><def.bfont_white>
			endif
		else
			ADDCLILOC 1060659,Owner,<def.bfont_lyellow><ref2.name><def.bfont_white>
		endif
		if <f_store_total_items <uid>> > 0
			ADDCLILOC 1060660,Items,<def.bfont_lyellow><f_store_total_items <uid>><def.bfont_white>
		endif
	else
		ADDCLILOC 1043304,<def.bfont_lyellow><f_numformat <room.tag.store_price>><def.bfont_white>
		ADDCLILOC 1060658,Max Vendors,<def.bfont_lyellow><room.tag0.max_vendors><def.bfont_white>
		ADDCLILOC 1060659,Max Lockdowns,<def.bfont_lyellow><room.tag0.max_lockdown><def.bfont_white>
		ADDCLILOC 1060660,Max Secured,<def.bfont_lyellow><room.tag0.max_secure><def.bfont_white>
	endif
	return 1

ON=@ContextMenuRequest
	if <isempty <tag.store_owner>>
		src.addcontextentry 680,1150656,020,055	// BUY (Store)
	else
		if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<uid>  // <f_is_store_owner <uid>,<src>>
			src.addcontextentry 666,1150328,020,055	// OWNER MENU
		endif
	endif
	if <src.isgm>
		src.addcontextentry 664,3002132,020,055	// Configuration (GM Menu)
	endif
ON=@ContextMenuSelect
	if <distance> > 3
		src.sysmessage @,,1 <name> is too far away.
		return 1
	endif
	if <argn>==664 && <src.isgm>	// GM Menu
		src.dialogclose d_store_gm
		dialog d_store_gm
		return 1
	endif
	if <argn>==680 					// BUY STORE
		if <src.gold> < <room.tag0.store_price>
			src.sysmessage @31,,1 You dont have <f_numformat <room.tag0.store_price>> gold.
			return 1
		endif
		src.gold -= <room.tag0.store_price>
		color=0
		tag.builton=<serv.rtime>
		serv.f_store_set_owner <uid>,<src>
		ref1=<src.account.tag.store>
		src.dialogclose d_store_owner
		ref1.dialog d_store_owner
	endif
	if <argn>==666 					// Owner Menu
		ref1=<src.account.tag.store>
		src.dialogclose d_store_owner
		ref1.dialog d_store_owner
		return 1
	endif

ON=@Dclick
	if <distance> > 3
		src.sysmessage <name> is too far away.
		return 1
	endif
	if <isempty <tag.store_owner>>
		src.sysmessage @,,1 <name> is for sale! <f_numformat <room.tag0.store_price>> gold.
	else
		if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<uid>
			src.dialogclose d_store_owner
			dialog d_store_owner
			DIALOG d_store_sign
		endif
	endif
	return 1

ON=@Dropon_Self
	return 1
	
ON=@Destroy
	room.name=<room.tag.default_name>
	if !<isempty <tag.store_owner>>
		serv.f_remove_all_vendors <uid>
		serv.f_recycle_store <uid>,<tag.store_owner>,<tag.recycle_pack>
		return 1
	endif
	
[FUNCTION f_recycle_store]
ref1=<argv[0]>	// sign uid
ref2=<argv[1]>	// owner uid
ref3=<argv[2]>	// recycle uid
if <ref1.isvalid> && <ref2.isvalid> && <ref3.isvalid>
	for 1 4
	if !<isempty <ref1.tag.vendor_pack_<local._for>>>
		ref4=<ref1.tag.vendor_pack_<local._for>>
		if <ref4.rescount>
			forcont <ref4> 10
			attr &=~ attr_move_never
			cont=<ref3>
			endfor
		endif
	endif
	endfor
	if <ref3.rescount>
		ref3.cont=<ref2.findlayer.29>
		ref3.contp=20,100
		ref3.attr &=~ attr_move_never
	endif
	if <ref1.tag0.gold> > 0
		serv.addgold <ref1.tag0.gold>,<ref2.findlayer.29>
	endif
	ref2.account.tag.store=
	ref1.remove
endif

	
	

[EOF]