[DIALOG d_store_gm]
300,200

PAGE 0
local.h=350
f_gumppic_dialog 152,42,294,<local.h>
local.font_title=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_title>>
local.font_text1=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_text1>>
local.font_text2=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_text2>>
local.font_red=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_red>>

dhtmlgump 190 51 220 20 0 0 <def.center><local.font_title><def.bfont_size7><name> <local.font_red>GM Panel
dhtmlgump 354 <eval <local.h>+16> 80 25 0 0 <def.div_right><local.font_text1><def.bfont_size4>$<local.font_title><f_numformat <tag0.gold>>

local.y=90
local.x=182
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Name
gumppictiled <eval <local.x>+36> <local.y> 185 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
dtextentrylimited <eval <local.x>+40> <local.y> 175 15 0480 1 26 <room.name>
button <eval <local.x>+225> <eval <local.y>+3> <def.store_small_button> 1 0 1
local.y += 22
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Price
gumppictiled <eval <local.x>+58> <local.y> 74 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
dtextentrylimited <eval <local.x>+64> <local.y> 64 15 0480 2 8 <room.tag0.store_price>
button <eval <local.x>+140> <eval <local.y>+3> <def.store_small_button> 1 0 2
local.y += 22
// dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Max Vendors
// gumppictiled <eval <local.x>+100> <local.y> 32 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
// dtextentrylimited <eval <local.x>+106> <local.y> 27 15 0480 3 2 <room.tag0.max_vendors>
// button <eval <local.x>+140> <eval <local.y>+3> <def.store_small_button> 1 0 3
// dhtmlgump <eval <local.x>+174> <local.y> 90 20 0 0 <local.font_text1>(<tag0.vendors> Active)
// local.y += 22
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Max Lockdowns
gumppictiled <eval <local.x>+100> <local.y> 32 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
dtextentrylimited <eval <local.x>+106> <local.y> 27 15 0480 4 2 <room.tag0.max_lockdown>
button <eval <local.x>+140> <eval <local.y>+3> <def.store_small_button> 1 0 4
dhtmlgump <eval <local.x>+174> <local.y> 90 20 0 0 <local.font_text1>(<tag0.lockdowns> Active)
local.y += 22
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Max Secure
gumppictiled <eval <local.x>+100> <local.y> 32 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
dtextentrylimited <eval <local.x>+106> <local.y> 27 15 0480 5 2 <room.tag0.max_secure>
button <eval <local.x>+140> <eval <local.y>+3> <def.store_small_button> 1 0 5
dhtmlgump <eval <local.x>+174> <local.y> 90 20 0 0 <local.font_text1>(<tag0.secure> Active)
local.y += 22

dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Store Settings
local.y += 25
dhtmlgump <local.x> <local.y> 100 20 0 0 <local.font_text1>Max Skins <local.font_text2><tag0.skins_used> of
gumppictiled <eval <local.x>+100> <local.y> 32 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
dtextentrylimited <eval <local.x>+106> <local.y> 27 15 0480 6 2 <tag0.max_skins>
button <eval <local.x>+140> <eval <local.y>+3> <def.store_small_button> 1 0 6
local.y += 22
dhtmlgump <local.x> <local.y> 100 20 0 0 <local.font_text1>Items <local.font_text2><f_store_total_items <uid>> of <eval <room.tag0.max_vendors>*<def.store_sell_max>>
local.y += 22
dhtmlgump <eval <local.x>+34> <eval <local.y>+2> 120 20 0 0 <local.font_title>Access Containers
button <local.x> <local.y> <def.store_go_button> 1 0 10
local.y += 22

ref1=<tag.store_owner>
if <ref1.isvalid>
	dhtmlgump <eval <local.x>+34> <eval <local.y>+2> 120 20 0 0 <local.font_title>Owner:
	button <local.x> <local.y> <def.store_go_button> 1 0 7	// set owner
	dhtmlgump <eval <local.x>+76> <eval <local.y>+2> 180 20 0 0 <local.font_text2><ref1.name>
	local.y += 22
	dhtmlgump <eval <local.x>+34> <eval <local.y>+2> 120 20 0 0 <local.font_title>Owner Menu
	button <local.x> <local.y> <def.store_go_button> 1 0 8
else
	dhtmlgump <eval <local.x>+34> <eval <local.y>+2> 120 20 0 0 <local.font_title>Set Owner
	button <local.x> <local.y> <def.store_go_button> 1 0 7
endif
local.y += 22
dhtmlgump <eval <local.x>+34> <eval <local.y>+2> 120 20 0 0 <local.font_title>Reset Store
button <local.x> <local.y> <def.store_go_button> 1 0 9


[DIALOG d_store_gm PREBUTTON]
src.dialogclose d_store_gm
if <argn1> > 0 && <argn1> <= 6
	if <isempty <argtxt[<argn1>]>> || (<isnum <argtxt[<argn1>]>> && <argtxt[<argn1>]> < 1)
		src.sysmessage @33,,1 Invalid Value.
		return 1
	endif
endif

[DIALOG d_store_gm BUTTON]
ON=0
	if !<isempty <tag.old_dispid>>
		dispid=<tag.old_dispid>
		tag.old_dispid=
		update
	endif
	return 1

ON=10  // BOARD MODE (access to packs)
	local.p=<p>
	tag.old_dispid=<dispid>
	dispid=01e5e
	p=<room.p>
	p=<local.p>
	dispid=<tag.old_dispid>
	tag.old_dispid=
	dialog d_store_gm
	update
	ref1=<uid>
	ref1.open

ON=1	// RENAME
	if <isnum <argtxt[1]>> || <eval strlen(<argtxt[1]>)> < 2
		src.sysmessage @33,,1 Invalid Name.
		dialog d_store_gm
		return 1
	endif
	room.name=<argtxt[1]>
	name=<argtxt[1]>
	resendtooltip 1,0
	src.sysmessage @52,,1 Store Renamed.
	for 1 <tag0.vendors>
	if !<isempty <tag.vendor_<local._for>>>
		ref1=<tag.vendor_<local._for>>
		ref1.resendtooltip 1,0
	endif
	endfor
	dialog d_store_gm
	return 1

ON=2	// PRICE
	if !<isnum <argtxt[2]>> || <argtxt[2]> > 99999999
		src.sysmessage @33,,1 Invalid Number.
		dialog d_store_gm
		return 1
	endif
	room.tag0.store_price=<argtxt[2]>
	src.sysmessage @52,,1 Store Price Updated.
	dialog d_store_gm
	resendtooltip 1,0
	return 1

ON=3  // SET MAX VENDORS
	if !<isnum <argtxt[3]>> || !(<argtxt[3]>==2 || <argtxt[3]>==4)
		src.sysmessage @33,,1 Invalid Number (2-4).
		dialog d_store_gm
		return 1
	endif
	tag.max_vendors=<argtxt[3]>
	src.sysmessage @52,,1 Store Max vendors updated to <argtxt[3]>.
	dialog d_store_gm
	resendtooltip 1,0
	return 1

ON=4	// MAX LOCKDOWNS
	if !<isnum <argtxt[4]>> || <argtxt[4]> > 999
		src.sysmessage @33,,1 Invalid Number.
		dialog d_store_gm
		return 1
	endif
	room.tag.max_lockdown=<argtxt[4]>
	src.sysmessage @52,,1 Store Max Lockdowns Updated to <argtxt[4]>.
	dialog d_store_gm
	resendtooltip 1,0
	return 1

ON=5	// MAX SECURED CONTAINERS
	if !<isnum <argtxt[5]>> || <argtxt[5]> > 99
		src.sysmessage @33,,1 Invalid Number.
		dialog d_store_gm
		return 1
	endif
	room.tag.max_lockdown=<argtxt[5]>
	src.sysmessage @52,,1 Store Max Containers Updated to <argtxt[5]>.
	dialog d_store_gm
	resendtooltip 1,0
	return 1

ON=6	// MAX SKINS
	if !<isnum <argtxt[6]>> || <argtxt[6]> > <def.store_skin_max>
		src.sysmessage @33,,1 Invalid Number.
		dialog d_store_gm
		return 1
	endif
	tag.max_skins=<argtxt[6]>
	src.sysmessage @52,,1 Store Max skins updated to <argtxt[6]>.
	dialog d_store_gm
	resendtooltip 1,0
	return 1

ON=7	// set owner
	src.targetf _store_set_owner <uid>
	src.message @,,1 Target New Owner
	return 1

ON=8	// owner menu
	ref1=<uid>
	ref1.dialog d_store_owner
	return 1

ON=9	// reset store
	local.p=<p>
	remove
	serv.newitem=i_store_sign
	new.p=<local.p>
	src.sysmessage @,,1 The store has been reset to default.
	return 1

ON=5555
    src.ctag0.dlg_bg += 1
    if <src.ctag.dlg_bg>==<def.dlg_bg_max>
        src.ctag.dlg_bg=
    endif
    dialog d_store_gm
    return 1

[EOF]