[EVENTS e_store_vendor]
ON=@ClientTooltip
	ref1=<tag.store_sign>
	ref2=<tag.vendor_pack>	// pack uid
	ref3=<ref1.tag.store_owner>
	ADDCLILOC 1042971,<def.bfont_size7><def.bfont_cyan><name><def.bfont_white><def.bfont_size4>	// vendor name
	ADDCLILOC 1114778,<def.bfont_lcyan><ref1.name><def.bfont_white>	// store name
	if <ref2.rescount> >=1
		ADDCLILOC 1060658,Items,<def.bfont_lyellow><ref2.rescount><def.bfont_white>	// # items
	endif
	ADDCLILOC 1061640,<def.bfont_lyellow><ref3.name><def.bfont_white>		// owner
	if !<isempty <ref2.tag.vendor_desc>>
		ADDCLILOC 1070722,<def.bfont_gray>"<ref2.tag.vendor_desc>"<def.bfont_white>
	endif
	return 1

ON=@ContextMenuRequest
	ref1=<tag.store_sign>
	ref2=<tag.vendor_pack>
	if <src.isgm>
		if <ref2.rescount>
			src.addcontextentry 681,3006103,020,055	// Buy
		endif
		src.addcontextentry 682,3006104,020,055	// Sell
		src.addcontextentry 683,1061829,020,055	// Relocate		
	else
		if (!<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1>)
			src.addcontextentry 682,3006104,020,055	// Sell
			src.addcontextentry 683,1061829,020,055	// Relocate
		else
			if <ref2.rescount>
				src.addcontextentry 681,3006103,020,055	// Buy
			endif
		endif
	endif

ON=@ContextMenuSelect
	if <distance> > 3
		src.sysmessage @,,1 <name> is too far away.
		return 1
	endif
	ref1=<tag.store_sign>
	ref2=<tag.vendor_pack>
	if <argn>==681 					// BUY MENU
		src.dialogclose d_store_buy
		src.ctag.vendor=<ref1>,<ref2>,<tag0.pack>
		ref1.dialog d_store_buy
		return 1
	endif
	if <argn>==682 					// SELL MENU
		src.dialogclose d_store_sell
		src.ctag.vendor=<ref1>,<ref2>,<tag0.pack>
		ref2.dialog d_store_sell
		return 1
	endif
	if <argn>==683 					// RELOCATE VENDOR
		src.dialogclose d_store_sell
		src.targetfg f_store_vendor_move <uid>
		return 1
	endif

ON=@Dclick
	if <distance> > 3
		src.sysmessage @,,1 <name> is too far away.
		return 1
	endif		
	src.dialogclose d_store_buy	// BUY MENU
	ref1=<tag.store_sign>
	ref2=<tag.vendor_pack>
	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1>
		src.ctag.vendor=<ref1>,<ref2>,<tag0.pack>
		ref1.dialog d_store_sell
	else
		if <ref2.rescount> > 0
			src.ctag.vendor=<ref1>,<ref2>,<tag0.pack>
			ref1.dialog d_store_buy
		endif
	endif
	return 1

ON=@Attack
	return 1
ON=@HitTry
	return 1
ON=@GetHit
	return 1
ON=@PetDesert
	return 1
ON=@SpellEffect
	return 1

ON=@Destroy
	ref1=<tag.store_sign>
	ref1.tag.vendor_<tag0.pack>=
	ref1.tag.vendors -= 1

[EVENTS e_store_vendor_equip]
ON=@ContextMenuRequest
	ref1=<tag.store_sign>
	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1> || <src.isgm>
		src.addcontextentry 669,1078184,020,055	// Equip Items
	endif
ON=@ContextMenuSelect
	if <distance> > 3
		src.sysmessage @,,1 <name> is too far away.
		return 1
	endif
	if <argn>==669
		src.targetf f_store_vendor_dress <uid>
	endif
ON=@Destroy
	ref1=<tag.store_sign>
	ref2=<ref1.tag.recycle_pack>
	if <ref1.isvalid> && <ref2.isvalid>
		for 1 24
		if !(<local._for>==21 || <local._for>==16 || <local._for>==11)
			if <findlayer.<local._for>.isvalid>
				findlayer.<local._for>.cont=<ref2>	// recycle pack inside sign
			endif
		endif
		endfor
	endif

[SPEECH spk_store_vendor]
ON=*buy
	ref1=<tag.store_sign>
	ref2=<tag.vendor_pack>
	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1>
		return 0
	else
		if <src.distance <uid>> < 3
			face <src>
			src.dialogclose d_store_buy
			src.ctag.vendor=<ref1>,<ref2>,<tag0.pack>
			ref1.dialog d_store_buy
		endif
	endif

ON=*sell
	ref1=<tag.store_sign>
	ref2=<tag.vendor_pack>
	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1>
		if <src.distance <uid>> < 3
			face <src>
			src.dialogclose d_store_sell
			src.ctag.vendor=<ref1>,<ref2>,<tag0.pack>
			ref1.dialog d_store_sell
		endif
	endif

// [SPEECH spk_store_cmds]	// added on player events
// ON=*lock this*
// 	ref1=<room.tag.store_sign>
// 	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1> || <src.isgm>
// 		src.targetf store_lockdown
// 		src.message @,,1 What do you wish to lockdown?
// 	endif

// ON=*secure this*
// 	ref1=<room.tag.store_sign>
// 	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1> || <src.isgm>
// 		src.targetf store_secure
// 		src.sysmessage What do you wish to secure?
// 	endif

// ON=*release this*
// 	ref1=<room.tag.store_sign>
// 	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1> || <src.isgm>
// 		src.targetf store_release
// 		src.sysmessage What do you wish to release?
// 	endif

[EOF]