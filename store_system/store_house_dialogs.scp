[DIALOG d_house_store_gm]
300,200

PAGE 0
local.font_title=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_title>>
local.font_text1=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_text1>>
local.font_text2=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_text2>>
local.font_red=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_red>>
local.h=230
f_gumppic_dialog 152,42,294,<local.h>
dhtmlgump 190 51 220 20 0 0 <def.center><local.font_title><def.bfont_size7><name> <local.font_red>GM Panel
dhtmlgump 354 <eval <local.h>+16> 80 25 0 0 <def.div_right><local.font_text1><def.bfont_size4>$<local.font_title><f_numformat <tag0.gold>>

ref1=<tag.store_owner>
local.y=90
local.x=182
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Name
gumppictiled <eval <local.x>+36> <local.y> 185 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
dtextentrylimited <eval <local.x>+40> <local.y> 175 15 0480 1 26 <name>
button <eval <local.x>+225> <eval <local.y>+3> <def.store_small_button> 1 0 1
local.y += 22
// dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Max Vendors
// gumppictiled <eval <local.x>+100> <local.y> 32 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
// dtextentrylimited <eval <local.x>+106> <local.y> 27 15 0480 3 2 <tag0.max_vendors>
// button <eval <local.x>+140> <eval <local.y>+3> <def.store_small_button> 1 0 3
// dhtmlgump <eval <local.x>+174> <local.y> 90 20 0 0 <def.bfont_gray>(<tag0.vendors> Active)
// local.y += 22
dhtmlgump <local.x> <local.y> 100 20 0 0 <local.font_title>Skins
dhtmlgump <eval <local.x>+76> <local.y> 180 20 0 0 <local.font_text2><tag0.skins_used> of
gumppictiled <eval <local.x>+106> <local.y> 22 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
dtextentrylimited <eval <local.x>+110> <local.y> 27 15 0480 6 2 <tag0.max_skins>
button <eval <local.x>+132> <eval <local.y>+3> <def.store_small_button> 1 0 2
local.y += 22
dhtmlgump <local.x> <local.y> 100 20 0 0 <local.font_title>Items
dhtmlgump <eval <local.x>+76> <local.y> 180 20 0 0 <local.font_text2><f_house_store_total_items <uid>> of <eval <def.store_house_vendor_max>*<def.store_sell_max>>
local.y += 22
dhtmlgump <local.x> <local.y> 100 20 0 0 <local.font_title>Owner
dhtmlgump <eval <local.x>+76> <local.y> 180 20 0 0 <local.font_text2><ref1.name>
local.y += 22
dhtmlgump <eval <local.x>+34> <eval <local.y>+2> 120 20 0 0 <local.font_title>Access Containers
button <local.x> <local.y> <def.store_go_button> 1 0 4
local.y += 22
dhtmlgump <eval <local.x>+34> <eval <local.y>+2> 120 20 0 0 <local.font_title>Owner Menu
button <local.x> <local.y> <def.store_go_button> 1 0 5
local.y += 22

[DIALOG d_house_store_gm PREBUTTON]
src.dialogclose d_house_store_gm
if <argn1> > 0 && <argn1> <= 3
	if <isempty <argtxt[<argn1>]>> || (<isnum <argtxt[<argn1>]>> && <argtxt[<argn1>]> < 1)
		src.sysmessage @33,,1 Invalid Value.
		return 1
	endif
endif

[DIALOG d_house_store_gm BUTTON]
ON=0
	if !<isempty <tag.old_dispid>>
		dispid=<tag.old_dispid>
		tag.old_dispid=
		update
	endif
	return 1

ON=1	// RENAME
	if <isnum <argtxt[1]>> || <eval strlen(<argtxt[1]>)> < 2
		src.sysmessage @33,,1 Invalid Name.
		dialog d_house_store_gm
		return 1
	endif
	name=<argtxt[1]>
	resendtooltip 1,0
	src.sysmessage @52,,1 Store Renamed.
	for 1 <tag0.vendors>
	if !<isempty <tag.vendor_<local._for>>>
		ref1=<tag.vendor_<local._for>>
		ref1.resendtooltip 1,0
	endif
	endfor
	dialog d_house_store_gm
	return 1

ON=2	// MAX SKINS
	if !<isnum <argtxt[2]>> || <argtxt[2]> > <def.store_skin_max>
		src.sysmessage @33,,1 Invalid Number.
		dialog d_house_store_gm
		return 1
	endif
	tag.max_skins=<argtxt[2]>
	src.sysmessage @52,,1 Store Max skins updated to <argtxt[2]>.
	dialog d_house_store_gm
	resendtooltip 1,0
	return 1

ON=3  // SET MAX VENDORS
	if !<isnum <argtxt[3]>> || <argtxt[3]> > 4
		src.sysmessage @33,,1 Invalid Number.
		dialog d_house_store_gm
		return 1
	endif
	tag.max_vendors=<argtxt[3]>
	src.sysmessage @52,,1 Store Max vendors updated to <argtxt[3]>.
	dialog d_house_store_gm
	resendtooltip 1,0
	return 1

ON=4  // BOARD MODE (access to packs)
	local.p=<p>
	tag.old_dispid=<dispid>
	dispid=01e5e
	p=<region.p>
	p=<local.p>
	dispid=<tag.old_dispid>
	tag.old_dispid=
	dialog d_house_store_gm
	update
	ref1=<uid>
	ref1.open

ON=5	// owner menu
	ref1=<uid>
	ref1.dialog d_house_store_owner
	return 1

ON=6	// reset store
	local.p=<p>
	remove
	serv.newitem=i_house_store_sign
	new.p=<local.p>
	src.sysmessage @,,1 The store has been reset to default.
	return 1

ON=5555
    src.ctag0.dlg_bg += 1
    if <src.ctag.dlg_bg>==<def.dlg_bg_max>
        src.ctag.dlg_bg=
    endif
    dialog d_house_store_gm
    return 1

[DIALOG d_house_store_owner]
300,200

PAGE 0
ref1=<src.account.tag.house_store>	// sign
if <src.isgm>
	ref1=<uid>	// sign
endif
ref2=<ref1.tag.store_owner>
ref5=<ref1.tag.recycle_pack>	// player recycle pack
local.font_title=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_title>>
local.font_text1=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_text1>>
local.font_text2=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_text2>>
local.font_red=<def.<def.dlg_bg_<src.ctag0.dlg_bg>_font_red>>
local.h=390
f_gumppic_dialog 152,42,294,<local.h>
dhtmlgump 190 51 220 20 0 0 <def.center><def.bfont_size7><local.font_title><ref1.name> <def.bfont_size4><local.font_text2>Settings
dhtmlgump 354 <eval <local.h>+16> 80 25 0 0 <def.div_right><local.font_text1><def.bfont_size4>$<local.font_title><f_numformat <ref1.tag0.gold>>

PAGE 1
local.y=96
local.x=182
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Name
gumppictiled <eval <local.x>+36> <local.y> 185 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
dtextentrylimited <eval <local.x>+40> <local.y> 175 15 0480 1 26 <ref1.name>
button <eval <local.x>+225> <eval <local.y>+3> <def.store_small_button> 1 0 100	// RENAME
local.y += 22
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Owner
dhtmlgump <eval <local.x>+106> <local.y> 128 20 0 0 <local.font_text2><ref2.name>
local.y += 22
dhtmlgump <local.x> <local.y> 100 20 0 0 <local.font_text1>Skins
dhtmlgump <eval <local.x>+106> <local.y> 100 20 0 0 <local.font_text2><ref1.tag0.skins_used><def.bfont_gray> of <local.font_text2><tag0.max_skins>
local.y += 22
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Max Vendors
dhtmlgump <eval <local.x>+106> <local.y> 27 20 0 0 <local.font_text2><ref1.tag0.max_vendors>
dhtmlgump <eval <local.x>+174> <local.y> 90 20 0 0 <def.bfont_gray>(<ref1.tag0.vendors> Active)
local.y += 22
dhtmlgump <local.x> <local.y> 120 20 0 0 <local.font_text1>Max Items
dhtmlgump <eval <local.x>+106> <local.y> 100 20 0 0 <local.font_text2><eval <def.store_house_vendor_max>*<def.store_sell_max>>
dhtmlgump <eval <local.x>+174> <local.y> 90 20 0 0 <def.bfont_gray>(Total <f_house_store_total_items <ref1>>)
local.y += 22
if <def0.store_vendor_tax>!=0
	dhtmlgump <local.x> <local.y> 120 20 0 0 <local.font_text1>Store Tax
	dhtmlgump <eval <local.x>+106> <local.y> 100 20 0 0 <local.font_text2><def.store_vendor_tax><def.bfont_gray>%
	local.y += 22
endif
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_text1>Created on
dhtmlgump <eval <local.x>+106> <local.y> 160 20 0 0 <def.bfont_size1><local.font_text2><ref1.tag.builton>
local.y += 22
if <ref5.rescount>
	dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 180 20 0 0 <local.font_red>Retrieve Recycled Items <def.bfont_gray>(<ref5.rescount>)
	button <local.x> <local.y> <def.store_go_button> 1 0 103
else
	dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 120 20 0 0 <local.font_title>Sign Settings
	button <local.x> <local.y> <def.store_go_button> 1 0 105
	local.y += 22
	dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 120 20 0 0 <local.font_title>Vendor Settings
	button <local.x> <local.y> <def.store_go_button> 0 2 0
	local.y += 22
	if <tag0.vendors>==0
		dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 120 20 0 0 <local.font_title>Spawn all Vendors
		button <local.x> <local.y> <def.store_add_button> 1 0 101
	else
		dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 120 20 0 0 <local.font_title>Remove all Vendors
		button <local.x> <local.y> <def.store_delvendor_button> 1 0 102
	endif
	if <tag0.gold> > 0
		local.y += 22
		dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 120 20 0 0 <local.font_title>Collect Gold
		button <local.x> <local.y> <def.store_go_button> 1 0 104
	endif
endif

PAGE 2
local.y=90
local.x=182

button <eval <local.x>+89> <eval <local.h>+16> 5603 5607 0 1 0	// back
if <ref1.tag0.max_vendors> > 2
    button <eval <local.x>+128> <eval <local.h>+16> 5601 5605 0 3 0	// next if big store
endif

dhtmlgump <local.x> <local.y> 220 20 0 0 <def.center><local.font_title>Vendor Settings
local.y += 22
for 1 2
ref3=<ref1.tag.vendor_pack_<local._for>>
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_title>Vendor <local._for>
if !<isempty <tag.vendor_<local._for>>>
	ref4=<ref1.tag.vendor_<local._for>>
	gumppictiled <eval <local.x>+88> <local.y> 146 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
	dtextentrylimited <eval <local.x>+92> <local.y> 130 15 0480 <eval <local._for>+50> 22 <ref4.name>	// 22 chars max
	button <eval <local.x>+68> <eval <local.y>+3> <def.store_small_button> 1 0 <eval <local._for>+50>	// RENAME VENDOR	
else
	dhtmlgump <eval <local.x>+92> <local.y> 100 20 0 0 <local.font_text2>Empty
endif
local.y += 22
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_title>Description
gumppictiled <eval <local.x>+88> <local.y> 146 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
button <eval <local.x>+68> <eval <local.y>+3> <def.store_small_button> 1 0 <eval <local._for>+30>	// SET DESCRIPTION VENDOR
if !<isempty <ref3.tag.vendor_desc>>
	dtextentrylimited <eval <local.x>+92> <local.y> 140 15 0480 <eval <local._for>+30> 21 <ref3.tag.vendor_desc>	// 22 chars max	
else
	dtextentrylimited <eval <local.x>+92> <local.y> 140 15 0480 <eval <local._for>+30> 21 Empty
endif
local.y += 22
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_title>Skin
dhtmlgump <eval <local.x>+92> <local.y> 100 20 0 0 <local.font_text2><getargv 1 <tag.skin_<ref3.tag0.skin_current>>>
local.y += 22
dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_title>Items
dhtmlgump <eval <local.x>+92> <local.y> 100 20 0 0 <local.font_text2><ref3.rescount><def.bfont_gray> of <ref1.dmore2h>
local.y += 22
if !<isempty <tag.vendor_<local._for>>>
	dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 90 20 0 0 <local.font_title>Remove
	button <local.x> <local.y> <def.store_delvendor_button> 1 0 <eval <local._for>+10>		//	REMOVE VENDOR
else
	dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 100 20 0 0 <local.font_title>Add
	button <local.x> <local.y> <def.store_add_button> 1 0 <local._for>					//	ADD VENDOR
endif
dhtmlgump <eval <local.x>+170> <eval <local.y>+3> 100 20 0 0 <local.font_title>Sell Menu
button <eval <local.x>+135> <local.y> <def.store_vend_desc_button> 1 0 <eval <local._for>+20>		//	SELL MENU
local.y += 22
// dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 90 20 0 0 <local.font_title>New Button <eval <local._for>+60>
// button <local.x> <local.y> <def.store_vend_desc_button> 1 0 <eval <local._for>+60>		//	EMPTY
dhtmlgump <eval <local.x>+170> <eval <local.y>+3> 100 20 0 0 <local.font_title>Skin List
button <eval <local.x>+135> <local.y> <def.store_go_button> 1 0 <eval <local._for>+40>		//	SKIN LIST
local.y += 28
endfor

if <ref1.tag0.max_vendors>==4

	PAGE 3
	local.y=100
	local.x=182
	button <eval <local.x>+89> <eval <local.y>+320> 5603 5607 0 2 0	// back
	// button <eval <local.x>+128> <eval <local.y>+320> 5601 5605 0 1 0	// next
	dhtmlgump <local.x> <local.y> 220 20 0 0 <def.center><local.font_title>Vendor Settings
	local.y += 22

	for 3 4
	ref3=<tag.vendor_pack_<local._for>>
	dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_title>Vendor <local._for>
	if !<isempty <tag.vendor_<local._for>>>
		ref4=<tag.vendor_<local._for>>
		gumppictiled <eval <local.x>+88> <local.y> 146 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
		dtextentrylimited <eval <local.x>+92> <local.y> 130 15 0480 <eval <local._for>+50> 22 <ref4.name>	// 22 chars max
		button <eval <local.x>+68> <eval <local.y>+3> <def.store_small_button> 1 0 <eval <local._for>+50>	// RENAME VENDOR	
	else
		dhtmlgump <eval <local.x>+92> <local.y> 100 20 0 0 <local.font_text2>Empty
	endif
	local.y += 22
	dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_title>Description
	gumppictiled <eval <local.x>+88> <local.y> 146 18 <def.dlg_bg_<src.ctag0.dlg_bg>_text_gump>
	button <eval <local.x>+68> <eval <local.y>+3> <def.store_small_button> 1 0 <eval <local._for>+30>	// SET DESCRIPTION VENDOR
	dtextentrylimited <eval <local.x>+92> <local.y> 130 15 0480 <eval <local._for>+30> 22 <qval <isempty <ref3.tag.vendor_desc>>?Empty:<ref3.tag.vendor_desc>>  // 22 chars max	
	local.y += 22
	dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_title>Skin
	dhtmlgump <eval <local.x>+92> <local.y> 100 20 0 0 <local.font_text2><getargv 1 <tag.skin_<ref3.tag0.skin_current>>>
	local.y += 22
	dhtmlgump <local.x> <local.y> 90 20 0 0 <local.font_title>Items
	dhtmlgump <eval <local.x>+92> <local.y> 100 20 0 0 <local.font_text2><ref3.rescount><def.bfont_gray> of <def.store_sell_max>
	local.y += 22
	if !<isempty <tag.vendor_<local._for>>>
		dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 90 20 0 0 <local.font_title>Remove
		button <local.x> <local.y> <def.store_delvendor_button> 1 0 <eval <local._for>+10>		//	REMOVE VENDOR
	else
		dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 100 20 0 0 <local.font_title>Add
		button <local.x> <local.y> <def.store_add_button> 1 0 <local._for>					//	ADD VENDOR
	endif
	dhtmlgump <eval <local.x>+170> <eval <local.y>+3> 100 20 0 0 <local.font_title>Sell Menu
	button <eval <local.x>+135> <local.y> <def.store_vend_desc_button> 1 0 <eval <local._for>+20>		//	SELL MENU
	local.y += 22
	// dhtmlgump <eval <local.x>+34> <eval <local.y>+3> 90 20 0 0 <local.font_title>New Button <eval <local._for>+60>
	// button <local.x> <local.y> <def.store_vend_desc_button> 1 0 <eval <local._for>+60>		//	EMPTY
	dhtmlgump <eval <local.x>+170> <eval <local.y>+3> 100 20 0 0 <local.font_title>Skin List
	button <eval <local.x>+135> <local.y> <def.store_go_button> 1 0 <eval <local._for>+40>		//	SKIN LIST
	local.y += 28
	endfor
endif

[DIALOG d_house_store_owner PREBUTTON]
src.dialogclose d_house_store_owner
if <region>!=<src.region>
	src.sysmessage @,,1 <name> is too far away.
	return 1
endif
if !<src.isgm> && <isempty <src.account.tag.store>> || <src.account.tag.store>!=<uid>
	return 1
endif

[DIALOG d_house_store_owner BUTTON]
ON=0
	if !<isempty <src.ctag.vendor>>
		ref1=<uid>	// sign
		src.ctag.vendor=
		ref1.dialog d_house_store_owner
	endif
 	return 1

ON=1 4		// ADD VENDOR
	ref1=<uid>	// sign
	local.p=<qval <argn1><=2?2:3>		// page #
	serv.f_add_vendor <argn1>,<ref1>
	ref1.dialog d_house_store_owner <local.p>
	return 1

ON=11 14	// REMOVE VENDOR
	ref1=<uid>	// sign
	local.v=<eval <argn1>-10>
	local.p=<qval <local.v><=2?2:3>		// page #
	serv.f_remove_vendor <local.v>,<ref1>
	ref1.dialog d_house_store_owner <local.p>
	return 1

ON=21 24	// SELL MENU
	src.dialogclose d_store_sell
	ref1=<uid>	// sign
	ref2=<ref1.tag.vendor_pack_<eval <argn1>-20>>
	src.ctag.vendor=<ref1>,<ref2>,<eval <argn1>-20>
	ref2.dialog d_store_sell
	return 1

ON=31 34	// VENDOR DESCRIPTION
	ref1=<uid>			// sign
	local.v=<eval <argn1>-30>
	local.p=<qval <local.v><=2?2:3>		// page #
    if <isempty <Argtxt[<argn1>]>> || <isnum <Argtxt[<argn1>]>>
        src.sysmessage @,,1 Invalid Text.
        ref1.dialog d_house_store_owner <local.p>
        return 1
    endif
	ref2=<ref1.tag.vendor_pack_<local.v>>	// store pack uid
	ref3=<ref1.tag.vendor_<local.v>>		// vendor uid
	ref2.tag.vendor_desc="<Argtxt[<argn1>]>"		// vendor desc for tooltip (stored in pack)
	ref3.resendtooltip 1,0
	src.sysmessage @,,1 Vendor <local.v> description has been set.
	ref1.dialog d_house_store_owner <local.p>
	return 1

ON=41 44	// SKIN
	src.dialogclose d_store_vendor_skin
	ref1=<uid>								// sign uid
	local.v=<eval <argn1>-40> 				// vendor #
	ref2=<ref1.tag.vendor_pack_<local.v>>	// store pack uid
	src.ctag.vendor=<ref1>,<ref2>,<local.v>
	ref1.dialog d_store_vendor_skin
	return 1

ON=51 54	// VENDOR RENAME
	ref1=<uid>	// sign
	local.v=<eval <argn1>-50>
	local.p=<qval <local.v><=2?2:3>		// page #
    if <isempty <Argtxt[<argn1>]>> || <isnum <Argtxt[<argn1>]>>
        src.sysmessage @,,1 Invalid Text.
        ref1.dialog d_house_store_owner <local.p>
        return 1
    endif
	ref2=<tag.vendor_<local.v>>	// store_pack #
	ref2.name=<Argtxt[<argn1>]>
	src.sysmessage @,,1 Vendor <local.v> has been renamed to <Argtxt[<argn1>]>.
	ref2.resendtooltip 1,0
	ref1.dialog d_house_store_owner <local.p>
	return 1

ON=61 64	// EMPTY
	return 1

ON=100	// STORE RENAME
	ref1=<uid>	// sign
	if <isnum <argtxt[1]>> || <eval strlen(<argtxt[1]>)> < 2
		src.sysmessage @33,,1 Invalid Name.
		ref1.dialog d_house_store_owner
		return 1
	endif
	ref1.name=<argtxt[1]>
	ref1.resendtooltip 1,0
	src.sysmessage @52,,1 Store Renamed.
	for 1 <ref1.tag0.vendors>
	if !<isempty <ref1.tag.vendor_<local._for>>>
		ref2=<ref1.tag.vendor_<local._for>>
		ref2.resendtooltip 1,0
	endif
	endfor
	ref1.dialog d_house_store_owner
	return 1

ON=101	// SPAWN ALL VENDORS
	ref1=<uid>	// sign
	serv.f_spawn_all_vendors <ref1>
	src.sysmessage @52,,1 All vendors activated.
	ref1.dialog d_house_store_owner
	return 1

ON=102	// REMOVE ALL VENDORS
	ref1=<uid>	// sign
	serv.f_remove_all_vendors <ref1>
	src.sysmessage @26,,1 All store vendors have been removed.
	ref1.dialog d_house_store_owner
	return 1

ON=103	// RETRIEVE RECYCLED
	ref1=<uid>	// sign
	ref2=<ref1.tag.recycle_pack>
	forcont <ref2> 0
	cont=<src.findlayer.21>
	endfor
	src.sysmessage @,,1 You retireve your store recycled items.
	ref1.dialog d_house_store_owner
	return 1

ON=104	// COLLECT GOLD
	ref1=<uid>	// sign
	if <ref1.tag0.gold> > 0
		if <ref1.tag0.gold> <= 50000
			src.sysmessage @,,1 You collect <f_numformat <ref1.tag0.gold>> gold from your store.
			src.newitem=i_gold,<ref1.tag0.gold>
		else
			src.sysmessage @,,1 You collect a check for <f_numformat <ref1.tag0.gold>> gold from your store.
			src.newitem=i_gold_check
			new.more1=<ref1.tag0.gold>
		endif			
		new.bounce <src>
		ref1.tag.gold=
	endif
	ref1.dialog d_house_store_owner
	return 1

ON=105	// SIGN SETTINGS
	dialog d_store_sign
	return 1

ON=5555
    ref1=<uid>	// sign
    src.ctag0.dlg_bg += 1
    if <src.ctag.dlg_bg>==<def.dlg_bg_max>
        src.ctag.dlg_bg=
    endif
    ref1.dialog d_house_store_owner
    return 1

[EOF]