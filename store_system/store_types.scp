[TYPEDEF t_store_vendor_gear]
ON=@ContextMenuRequest
	ref1=<tag.store_sign>
	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1> || <src.isgm>
		src.addcontextentry 670,1078185,020,055	// Unequip Items
	endif

ON=@ContextMenuSelect
	if <distance> > 3
		src.sysmessage @,,1 <name> is too far away.
		return 1
	endif
	if <argn>==670
		ref1=<uid>
		ref1.bounce <src>
	endif
	return 1

ON=@Unequip
	events=-t_store_vendor_gear


[TYPEDEF t_store_lockdown]
ON=@ClientTooltip
	ADDCLILOC 501643
ON=@PickUp_Ground   
	if !<src.Isgm>
		return 1
	endif
ON=@PickUp_Self
	if !<src.Isgm>
		return 1
	endif
ON=@Equiptest
	if <src.isplayer>
		src.sysmessage @,,1 <name> cannot be equipped.
		return 1
	endif
ON=@Dclick
	if <distance> > 2
		return 1
	endif
	if !<src.findid.i_itemlore_cd.isvalid>
		if <isweapon> || <isarmor>
			local.skill=Armslore
		else
			local.skill=Itemid
		endif
		if <src.<local.skill>> < 100.0
			src.skillgain <local.skill>
			src.newitem=i_itemlore_cd
			new.equip <src>
		endif
	endif
	src.ctag.item_lore=<uid>
	dialog d_item_lore
	return 1

// ON=@Destroy

// ***********************
[TYPEDEF t_store_secure]
ON=@ClientTooltip
	ADDCLILOC 1065550,Secured

ON=@PickUp_Ground
	return 1

ON=@PickUp_Self
	ref1=<room.tag.store_sign>
	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1> || <src.isgm>
		return 0
	else
		src.sysmessage That is secured.
		return 1
	endif

ON=@Dropon_Self
	ref1=<room.tag.store_sign>
	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1> || <src.isgm>
		return 0
	else
		src.sysmessage That is secured.
		return 1
	endif

ON=@Dclick
	ref1=<room.tag.store_sign>
	if !<isempty <src.account.tag.store>> && <src.account.tag.store>==<ref1> || <src.isgm>
		return 0
	else
		src.sysmessage That is secured.
		return 1
	endif

// ON=@Destroy

[TYPEDEF t_store_skin]
ON=@ClientTooltip
	ADDCLILOC 1042971,<def.bfont_lyellow>Store Skin<def.bfont_white>
	ADDCLILOC 1070722,<def.bfont_gray><getargv 3 <tag.skin>><def.bfont_white>

ON=@Dclick
	if <topobj>!=<src> || <isinbank>
		src.sysmessage @,,1 You can't reach <name>.
		return 1
	endif
	if <isempty <src.account.tag.store>>
		src.sysmessage @,,1 You don't have a store.
		return 1
	endif

	ref1=<src.account.tag.store>	// sign
	if <ref1.tag0.skins_used>==<ref1.tag0.max_skins>
		src.sysmessage @,,1 Your store has no free slots for skins.
		return 1
	endif
	for 1 <ref1.tag0.max_skins>
	if !<isempty <ref1.tag.skin_<local._for>>>
		if !(strcmpi(<getargv 0 <ref1.tag.skin_<local._for>>>,<getargv 0 <tag.skin>>))
			src.sysmessage @,,1 Your store already has that skin.
			return 1
		endif
	else
		if !<local.free_slot>
			local.free_slot=<local._for>
		endif
	endif
	endfor
	ref1.tag.skin_<local.free_slot>=<tag.skin>
	ref1.tag0.skins_used += 1
	src.sysmessage @,,1 Store Skin <name> has been added to your store slot <local.free_slot>.
	src.sound=575 	// quest complete
	src.dialogclose d_store_vendor_skin
	remove
	return 1

[EOF]