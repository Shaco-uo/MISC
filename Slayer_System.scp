[DEFNAME SLAYER_SYSTEM]
slayer_hit_chance           33      // 33%
slayer_hit_fx               i_fx_swirly_sparks
slayer_hit_snd              486

[EVENTS e_slayer_weapon]    // check how to add spellhit instead of using spelldef
ON=@Hit
    if !<isempty <src.tag.SlayerGroup>>
        if strmatch(*<src.tag.SlayerGroup>*,<argo.tag.slayer1>) 
            local.slayer=1
        endif
        if strmatch(*<src.tag.SlayerGroup>*,<argo.tag.slayer2>)
            local.slayer=2
        endif
        if strmatch(*<src.tag.SlayerGroup>*,<argo.tag.slayer3>)
            local.slayer=3
        endif
        if <local.slayer>   // && rand(100) <= <def.slayer_hit_chance>
            // say hit <argo.tag.slayer<local.slayer>> <eval <argo.tag.slayer_dam<local.slayer>>> // debug
            src.damage <eval <argo.tag.slayer_dam<local.slayer>>> <def.dam_magic> <uid>
            src.effect=3,<def.slayer_hit_fx>,10,20,10,color_<argo.tag.slayer<local.slayer>>
            src.sound=<def.slayer_hit_snd>
        endif
    endif

[TYPEDEF t_slayer_weapon]
ON=@Clienttooltip    // added at main tooltip script
    if !<isempty <tag.slayer1>>
        ADDCLILOC 1060658,<eval <tag.slayer_dam1>>,<tag.slayer1>
    endif
    if !<isempty <tag.slayer2>>
        ADDCLILOC 1060659,<eval <tag.slayer_dam2>>,<tag.slayer2>
    endif
    if !<isempty <tag.slayer3>>
        ADDCLILOC 1060660,<eval <tag.slayer_dam3>>,<tag.slayer3>
    endif
ON=@Equip
    src.events +e_slayer_weapon
ON=@Unequip 
    src.events -e_slayer_weapon

[FUNCTION f_setbonus_slayer] // usage on @create: f_setbonus_slayer Dragon,15
if <isempty <tag.slayer1>>
    local.t=1
    events=+t_slayer_weapon
else
    if <isempty <tag.slayer2>>
        local.t=2
    else
        if <isempty <tag.slayer3>>
            local.t=3
        else
            serv.log <baseid> <name> <uid> has more than 3 slayer bonus.
            return 1
        endif
    endif
endif
tag.slayer<local.t>=<argv[0]>
tag.slayer_dam<local.t>=<argv[1]>

[ITEMDEF i_pike_lavitz] 
DEFNAME=i_pike_lavitz 
ID=i_Pike
NAME=Lavitz Pike
TYPE=t_weapon_fence
Layer=2
SKILL=Fencing 
TWOHANDS=Y
Layer=2
DAM=40,48
SPEED=38
REQSTR=150
WEIGHT=17.0
VALUE=1000
FLIP=1
RESOURCES=40 i_ingot_iron
SKILLMAKE=Blacksmithing 100.0,Armslore 100.0
ON=@Create
    COLOR=07a1
    hitpoints={45 55}
    f_setbonus_slayer Undead,12
    f_setbonus_slayer Demon,12

[EOF]