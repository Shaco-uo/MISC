[PLEVEL 2]
tester

[FUNCTION tester]
src.dialogclose d_spheretester
sdialog d_spheretester

[Dialog d_SphereTester]
50,50

Page 0 // Basic layout
Resizepic 0 0 5054 652 507
Gumppictiled 5 5 640 497 2604
Gumppictiled 5 5 640 25 2624
Gumppictiled 5 35 640 467 2624
Checkertrans 5 5 640 497
DText 10 7 0c1 SphereServer <src.Version> Tester Panel (<Serv.Servname>)
Button 615 5 4017 4018 1 0 0

Page 1
local.x=10
local.y=50
DText <local.x> <local.y> 0c1 StatTotal
DText <eval <local.x>+68> <local.y> 0480 <src.isstattotal>
local.y += 20
DText <local.x> <local.y> 0c1 Stat Max
DText <eval <local.x>+68> <local.y> 998 <src.skillclass.STATSUM>
local.y += 40
DText <local.x> <local.y> 0c1 Strenght
DText <eval <local.x>+60> <local.y> 998 (Max <src.skillclass.str>)
local.y += 25
gumppictiled <eval <local.x>+52> <local.y> 58 21 87
Checkertrans <eval <local.x>+54> <eval <local.y>+2> 54 17
DTextenTrylimited <eval <local.x>+62> <local.y> 40 15 0480 101 3 <src.str>
Button <eval <local.x>+20> <local.y> 4005 4007 1 0 101
local.y += 30
DText <local.x> <local.y> 0c1 Dexterity
DText <eval <local.x>+60> <local.y> 998 (Max <src.skillclass.dex>)
local.y += 25
gumppictiled <eval <local.x>+52> <local.y> 58 21 87
Checkertrans <eval <local.x>+54> <eval <local.y>+2> 54 17
DTextenTrylimited <eval <local.x>+62> <local.y> 40 15 0480 102 3 <src.dex>
Button <eval <local.x>+20> <local.y> 4005 4007 1 0 102
local.y += 30
DText <local.x> <local.y> 0c1 Intellect
DText <eval <local.x>+60> <local.y> 998 (Max <src.skillclass.int>)
local.y += 25
gumppictiled <eval <local.x>+52> <local.y> 58 21 87
Checkertrans <eval <local.x>+54> <eval <local.y>+2> 54 17
DTextenTrylimited <eval <local.x>+62> <local.y> 40 15 0480 103 3 <src.int>
Button <eval <local.x>+20> <local.y> 4005 4007 1 0 103
local.y += 50
DText <local.x> <local.y> 0c1 Skill Total
DText <eval <local.x>+68> <local.y> 0480 <Fval <SkillTotal>>
local.y += 20
DText <local.x> <local.y> 0c1 Skill Max
DText <eval <local.x>+68> <local.y> 998 <Fval <src.skillclass.Skillsum>>

dorigin 160 16
For x 0 57
    if !(<local.x>==50 || <local.x>==52 || <local.x>==53 || <local.x>==54 || <local.x>==55 || <local.x>==57) // Focus,Bushido,Ninjitsu,Spellweaving,Mysticism,Throwing
	if (<Local.x>==18) 
		dorigin 324 16
	Elif (<Local.x>==36)
		dorigin 488 16
	Endif
	DText +0 *23 0480 <serv.skill.<Local.x>.key>
	gumppictiled +104 +0 50 21 87
	Checkertrans +106 +2 46 17
	DTextenTrylimited +112 +0 40 15 0480 <dLocal.x> 5 <fval <<serv.skill.<dLocal.x>.key>>>
    Button +86 +3 1209 1210 1 0 <eval <dLocal.x>+1>
    endif
Endfor

[Dialog d_SphereTester button]
On=0
    src.dialogclose d_SphereTester
    return 1
on=1 58
    src.dialogclose d_SphereTester
    if <isempty <Argtxt[<eval <argn1>-1>]>> || !<isnum <Argtxt[<eval <argn1>-1>]>> || <Argtxt[<eval <argn1>-1>]> < 0
        src.sysmessage @,,1 Invalid Number.
        Dialog d_SphereTester
        return 1
    elif <fval <Argtxt[<eval <argn1>-1>]>> > <src.<serv.skill.<eval <argn1>-1>.key>>
        if <eval (<Argtxt[<eval <argn1>-1>]>-<src.<serv.skill.<eval <argn1>-1>.key>>)+<src.SkillTotal>> > <src.skillclass.Skillsum>
            src.sysmessage @,,1 SkillCap Reached.
            Dialog d_SphereTester
            return 1
        endif
    elif <src.<serv.skill.<eval <argn1>-1>.key>> > <src.skillclass.<serv.skill.<eval <argn1>-1>.key>>
		src.sysmessage @,,1 You can't raise <serv.skill.<eval <argn1>-1>.key> any higher than <src.skillclass.<serv.skill.<eval <argn1>-1>.key>>.
	    Dialog d_SphereTester
	    return 1
    endif   
    src.<serv.skill.<eval <argn1>-1>.key>=<Argtxt[<eval <argn1>-1>]>
    // src.sysmessage @,,1 Your <serv.skill.<eval <argn1>-1>.key> is now <fval <Argtxt[<eval <argn1>-1>]>>.
    Dialog d_SphereTester
    return 1

On=101 103
    src.dialogclose d_SphereTester
    if <isempty <Argtxt[<argn1>]>> || !<isnum <Argtxt[<argn1>]>> || <Argtxt[<argn1>]> < 1
        src.sysmessage @,,1 Invalid Number.
        Dialog d_SphereTester
        return 1
    endif
    if <argn1>==101
        local.stat=Str
        local.s=<Argtxt[<argn1>]>+<src.int>+<src.dex>
    elif <argn1>==102
        local.stat=Dex
        local.s=<Argtxt[<argn1>]>+<src.int>+<src.str>
    elif <argn1>==103
        local.stat=Int
        local.s=<Argtxt[<argn1>]>+<src.str>+<src.dex>
    endif
    if <Argtxt[<argn1>]> > <src.skillclass.<local.stat>>
        src.sysmessage @,,1 Invalid Number (Max <src.skillclass.<local.stat>>).
        Dialog d_SphereTester
        return 1
    elif <Argtxt[<argn1>]> > <src.<local.stat>>
        if <local.s> > <src.skillclass.statsum>
            src.sysmessage @<def.tcolor_red> Max Stats Reached.
            Dialog d_SphereTester
            return 1
        endif
    endif
    src.<local.stat>=<Argtxt[<argn1>]>
    // src.sysmessage @,,1 Your <local.stat> is now <Argtxt[<argn1>]>.
    Dialog d_SphereTester

[EOF]