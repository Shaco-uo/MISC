[DEFNAME all_mounts]
//* Function f_getdefs will create a file (local.file) with all the defs needed by the dialog
//* add mount baseid's into categories, first element in list must be the category name.
//* copy created defs into this file to keep it clean.
//* After defs generation some short description for the created defs can be added at the end
//* of every item line (as a new <arg>).

mounts_1_id         Common,c_horse_gray,c_horse_brown_dk,c_horse_brown_lt,c_horse_tan,c_zos_savage,c_ostard_frenzied,c_ostard_desert,c_llama
mounts_2_id         Steeds,c_demon_steed,c_steed_dark,c_fire_horse,c_ice_horse,c_steed_gryphon,c_horse_sea,c_horse_snow,c_mustang,c_nightmare,c_nightmare_old,c_orden,c_steed_ranger,c_steed_silver,c_zebra,c_steed_armored,c_horse_palomino
mounts_3_id         Zostrich,c_zos_aqua,c_zos_rock,c_zos_blue,c_zos_ice,c_zos_meld,c_zos_mytheril,c_zos_orange,c_zos_ranger,c_zos_red
mounts_4_id         Ostard,c_ostard_forest,c_ostard_silver,c_ostard_vulcan
mounts_5_id         Oclock,c_oclock_forest,c_oclock_frost,c_oclock_silver,c_oclock_vulcan
mounts_6_id         Llama,c_blood_llama,c_shadow_llama,c_sky_llama,c_sun_llama,c_black_llama,c_war_llama
mounts_7_id		    Kaos,c_steed_kaos,c_zos_kaos,c_oclock_kaos,c_llama_kaos,c_cusidhe_kaos
mounts_8_id         Misc,c_bear_polar,c_giant_beetle,c_hiryu,c_kirin,c_reptalon,c_ridgeback,c_savage_ridgeback,c_swamp_dragon,c_unicorn
mounts_9_id			Rare,c_eowmu,c_lasher,c_war_boar_mount,c_raptor_aqua,c_war_bear,c_bear_white,c_beetle_fire,c_boura_armoured,c_gryphon,c_gryphon_white,c_hawkstrider,c_lion,c_panda,c_talbuk,c_tawny_windrider,c_horse_undead,c_wolf_black,c_frost_wolf,c_raptor_green,c_raptor_purple
mounts_10_id		Exotic,c_capybara_mount,c_frost_mite,c_camel,c_camaleon,c_drake_mount
mounts_11_id		Evil,c_coconut_crab_mount,c_cu_sidhe,c_skeletal_mount,c_skeletal_cat,c_hellhound_ancient,c_tarantula,c_tiger_wild // c_serpentine_dragon_ethereal nonmover
mounts_12_id		Dogs,c_dog_malamut,c_dog_bernard,c_dog_terier,c_dog_dane,c_dog_newfoundland,c_dog_rottweiler

[function f_getdefs]    // create new def lists using defs above
local.list=1    // set list number
file.close
file.mode.append = 1
local.file = "Scripts/new.scp"  // file for the new defs

if <file.open <local.file>>
    file.writeline "[DEFNAME mount_book]"
    for 1 12
        file.writeline "mounts_<dlocal.list>    <strarg <def.mounts_<dlocal.list>_id>>,<eval <GetArgCount <def.mounts_<dlocal.list>_id>>-1>"
        args=<def.mounts_<dlocal.list>_id>
        for 1 <eval <argv>-1>
        local.m=<argv[<dlocal._for>]>   // baseid
        serv.log <local.m>
        serv.newnpc <local.m>
        ref1=<new>
        if !<isempty <ref1.tag.mount_stat>>
            local.bonus= <ref1.tag.mount_stat> +<ref1.dtag0.mount_stat_amt>
        elif !<isempty <ref1.tag.mount_armor>>
            local.bonus=Armor +<ref1.tag0.mount_armor>
        elif !<isempty <ref1.tag.mount_skill>>
            local.bonus=<ref1.tag.mount_skill> +<fval <ref1.dtag0.mount_skill_amt>>
        else
            local.bonus=0
        endif
        file.writeline "mounts_<dlocal.list>_<dlocal._for>      <local.m>,<ref1.name>,<eval <ref1.icon>>,<local.bonus>,<ref1.taming>,<ref1.followerslots>,<eval <ref1.color>>,<ref1.str>,<ref1.hits>,<ref1.dex>,<ref1.int>,<ref1.wrestling>,<ref1.tactics>,<ref1.magicresistance>,<ref1.parrying>,<ref1.magery>"
        ref1.remove
        endfor
        file.writeline " "
        local.list ++
    endfor
    file.close
endif

// ****************************************
[DEFNAME mount_book]
//* Uncomment for vanilla. Needs some uncommenting further above, in the tilepic part also
//* needs a little coord adjust since I use custom function for centering icons.
// dlg_btn_green_1	    2224 2224
// dlg_btn_blue_1	    2224 2224
// bfont_lblack    	<basefont color="#383838">      // pale black
// bfont_brown			<basefont color="#715d31">	    // brown book
// bfont_dbrown		<basefont color="#42361c">	    // brown book
// *****************************************
//* These defs must be replaced for the ones generated by the function

// mounts_1     Common,8
// mounts_2     Steeds,16
// mounts_3     Zostrich,9
// mounts_4     Ostard,3
// mounts_5     Oclock,4
// mounts_6     Llama,6
// mounts_7     Kaos,5
// mounts_8     Misc,9
// mounts_9     Rare,20
// mounts_10    Exotic,5
// mounts_11    Evil,7
// mounts_12    Dogs,6

// mounts_1_1      c_horse_gray,Gray Horse,8479,0,39.1,1,0,23,137,73,6,38.9,41.6,28.2,0.0,0.0
// mounts_1_2      c_horse_brown_dk,Dark Brown Horse,8481,0,39.1,1,0,97,140,70,7,35.4,31.6,27.5,0.0,0.0
// mounts_1_3      c_horse_brown_lt,Light Brown Horse,8480,0,39.1,1,0,87,141,56,8,38.6,43.0,30.0,0.0,0.0
// mounts_1_4      c_horse_tan,Tan Horse,8484,0,39.1,1,0,83,135,56,10,37.4,41.9,28.6,0.0,0.0
// mounts_1_5      c_zos_savage,Savage Zostrich,8502,0,75.0,1,028d,164,199,90,8,89.0,99.8,99.0,0.0,0.0,"Random Color"
// mounts_1_6      c_ostard_frenzied,Zostrich,8502,0,70.0,1,0460,104,183,94,9,54.0,53.2,17.6,0.0,0.0,"Random Color"
// mounts_1_7      c_ostard_desert,Oclock,8501,0,65.1,1,0,125,172,65,6,36.5,26.2,28.9,0.0,0.0
// mounts_1_8      c_llama,Llama,8438,0,48.1,1,0,88,128,57,7,36.4,34.4,28.6,0.0,0.0

// mounts_2_1      c_demon_steed,Daemon Steed,8484,Magery +5.0,120.0,2,0794,289,219,105,94,81.0,32.0,27.2,35.4,70.0
// mounts_2_2      c_steed_dark,Dark Steed,9628,Str +6,105.0,2,03e6,344,311,101,125,87.2,98.3,84.4,0.0,0.0
// mounts_2_3      c_fire_horse,Fire Steed,8484,Magery +4.0,109.0,1,0793,187,227,90,109,38.9,42.5,25.9,44.5,50.0
// mounts_2_4      c_ice_horse,Frost Steed,8484,Magery +4.0,109.0,1,0480,194,222,98,125,37.1,35.2,27.8,41.1,50.0
// mounts_2_5      c_steed_gryphon,Gryphon,8689,Tactics +3.0,110.0,2,07ad,288,306,102,97,34.8,41.4,28.9,36.6,0.0
// mounts_2_6      c_horse_sea,Sea Horse,8480,Str +4,115.0,1,0c8,261,197,99,130,63.2,67.6,72.8,0.0,0.0,"Only moves on water"
// mounts_2_7      c_horse_snow,Snow Horse,8484,Str +2,65.0,1,0481,148,206,135,30,73.9,63.3,26.4,37.6,0.0
// mounts_2_8      c_mustang,Mustang,8480,0,78.0,1,0263,160,204,94,10,38.0,30.2,27.9,36.4,0.0
// mounts_2_9      c_nightmare,Nightmare,26668,Magery +5.0,110.0,2,0,254,301,95,38,61.5,75.1,73.1,48.0,56.5
// mounts_2_10      c_nightmare_old,Ancient Nightmare,9628,Magery +5.0,115.0,2,09e8,331,308,93,91,84.1,99.3,89.7,0.0,30.6
// mounts_2_11      c_orden,Orden,8481,Magery +5.0,110.0,1,07a1,234,298,90,107,32.2,33.4,27.3,41.9,0.0
// mounts_2_12      c_steed_ranger,Ranger Steed,8481,Archery +4.0,95.0,1,01cf,182,312,87,116,34.0,37.0,28.0,36.2,0.0
// mounts_2_13      c_steed_silver,Silver Steed,8480,0,97.0,1,0482,223,311,103,94,81.2,97.1,91.1,0.0,0.0
// mounts_2_14      c_zebra,Zebra,26646,Dex +5,115.0,1,0,135,207,89,18,82.2,61.5,62.7,0.0,0.0
// mounts_2_15      c_steed_armored,Armored Steed,11676,Armor +25,120.0,1,0,337,373,142,4,85.5,79.4,27.1,49.5,0.0
// mounts_2_16      c_horse_palomino,Palomino Horse,40662,0,115.1,1,0,183,224,85,10,56.3,47.2,8.3,0.0,0.0

// mounts_3_1      c_zos_aqua,Aqua Zostrich,8502,Dex +4,95.0,1,055,152,183,97,8,69.5,32.9,29.1,41.0,0.0
// mounts_3_2      c_zos_rock,Bloodrock Zostrich,8502,Dex +4,95.0,1,04c2,170,174,104,10,69.1,20.4,28.9,49.7,0.0
// mounts_3_3      c_zos_blue,Blue Zostrich,8502,Dex +4,95.0,1,0555,159,190,104,9,74.0,20.0,29.4,41.3,0.0
// mounts_3_4      c_zos_ice,Ice Zostrich,8502,Dex +4,95.0,1,0480,99,181,112,6,70.5,27.9,28.5,45.9,0.0
// mounts_3_5      c_zos_meld,Meld Zostrich,8502,Dex +4,95.0,1,08030,94,180,105,7,70.1,27.3,25.5,47.1,0.0
// mounts_3_6      c_zos_mytheril,Mytheril Zostrich,8502,Dex +4,95.0,1,052b,105,188,107,10,71.5,31.6,29.7,43.6,0.0
// mounts_3_7      c_zos_orange,Orange Zostrich,8502,Dex +4,95.0,1,027,129,185,110,8,70.6,31.1,27.9,40.6,0.0
// mounts_3_8      c_zos_ranger,Ranger Zostrich,8502,Dex +4,95.0,1,0599,144,174,107,7,69.8,27.7,26.7,48.1,0.0
// mounts_3_9      c_zos_red,Deep Red Zostrich,8502,Dex +4,95.0,1,079c,103,190,114,8,70.8,29.3,27.6,43.8,0.0

// mounts_4_1      c_ostard_forest,Forest Ostard,8503,0,65.1,1,08ad,172,199,100,11,63.0,38.7,16.3,26.0,0.0
// mounts_4_2      c_ostard_silver,Silver Ostard,8503,Dex +4,85.0,1,07a1,159,245,104,10,82.1,59.8,84.0,49.7,0.0
// mounts_4_3      c_ostard_vulcan,Vulcan Ostard,8503,Dex +5,105.0,2,01,292,246,101,9,82.7,58.7,92.9,40.8,0.0

// mounts_5_1      c_oclock_forest,Forest Oclock,8501,Dex +4,79.0,1,0795,165,214,99,1,62.4,49.8,15.7,25.8,0.0
// mounts_5_2      c_oclock_frost,Frost Oclock,8501,Int +5,100.0,1,0480,161,210,88,1,60.1,47.3,18.5,27.5,0.0
// mounts_5_3      c_oclock_silver,Silver Oclock,8501,Dex +5,98.0,1,07a1,175,225,127,1,71.9,49.2,16.8,28.6,0.0
// mounts_5_4      c_oclock_vulcan,Vulcan Oclock,8501,Int +5,105.0,2,01,290,245,130,1,72.7,47.3,14.6,29.1,0.0

// mounts_6_1      c_blood_llama,Blood Llama,8438,Int +4,95.0,1,0a26,81,166,81,22,26.5,23.6,18.2,44.8,0.0
// mounts_6_2      c_shadow_llama,Shadow Llama,8438,Int +4,95.0,1,0770,184,246,127,1,57.1,24.2,70.7,36.5,0.0
// mounts_6_3      c_sky_llama,Sky Llama,8438,Int +4,95.0,1,07a0,77,194,80,19,25.5,26.5,16.8,39.5,0.0
// mounts_6_4      c_sun_llama,Sun Llama,8438,Int +4,95.0,1,07ad,77,165,82,25,26.1,22.1,18.5,41.6,0.0
// mounts_6_5      c_black_llama,Vulcan Llama,8438,Int +5,105.0,2,01,289,276,144,17,28.8,19.9,15.5,36.0,0.0
// mounts_6_6      c_war_llama,War Llama,8438,Tactics +3.0,95.0,1,0794,273,268,157,3,69.7,52.6,39.2,45.3,0.0

// mounts_7_1      c_steed_kaos,Kaos Steed,8484,Magery +4.0,110.0,1,07ab,224,302,88,118,82.8,98.8,88.7,61.5,60.0,"Random rare color"
// mounts_7_2      c_zos_kaos,Kaos Zostrich,8502,Magery +4.0,110.0,1,0af7,223,312,98,97,88.1,97.6,93.8,61.8,60.0,"Random rare color"
// mounts_7_3      c_oclock_kaos,Kaos Oclock,8501,Magery +4.0,110.0,1,077b,219,305,92,94,80.0,97.4,85.3,61.7,60.0,"Random rare color"
// mounts_7_4      c_llama_kaos,Kaos Llama,8438,Magery +4.0,110.0,1,0ae9,216,315,99,101,82.1,98.8,97.0,61.3,60.0,"Random rare color"
// mounts_7_5      c_cusidhe_kaos,Kaos Cu Sidhe,11670,Magery +4.0,110.0,1,0a08,217,300,102,97,89.5,100.0,84.6,62.0,60.0,"Random rare color"

// mounts_8_1      c_bear_polar,Polar Bear,8417,Str +3,95.0,1,0,178,231,128,46,63.6,50.9,48.4,54.8,0.0
// mounts_8_2      c_giant_beetle,Giant Beetle,9743,Str +5,100.0,2,0,245,312,105,1,78.5,75.2,66.8,74.7,0.0,"Usable backpack, max weight 2.500"
// mounts_8_3      c_hiryu,Hiryu,10090,Tactics +3.0,120.0,2,0,200,321,128,224,88.4,72.8,27.0,47.8,0.0
// mounts_8_4      c_kirin,Kirin,9632,Magery +4.0,115.0,1,0,181,246,101,48,99.9,99.5,99.4,65.1,45.4
// mounts_8_5      c_reptalon,Reptalon,11669,Str +5,140.0,2,0,450,394,164,32,86.8,72.6,85.2,28.5,0.0
// mounts_8_6      c_ridgeback,Ridgeback,9749,Dex +4,90.0,1,0,139,139,108,7,93.4,91.3,77.7,79.5,0.0
// mounts_8_7      c_savage_ridgeback,Savage Ridgeback,9749,0,89.0,1,0,172,224,128,1,72.9,48.3,19.7,24.2,0.0
// mounts_8_8      c_swamp_dragon,Swamp Dragon,9753,Str +5,100.0,1,05a1,216,299,91,1,82.0,98.2,94.1,58.6,0.0
// mounts_8_9      c_unicorn,Unicorn,9678,Int +6,149.0,2,0,239,256,106,55,45.3,49.7,27.3,41.9,55.8

// mounts_9_1      c_eowmu,Eowmu,41152,Magery +3.0,120.1,1,0,135,265,78,45,43.2,44.9,78.0,0.0,0.0
// mounts_9_2      c_lasher,Lasher,40501,0,115.1,1,0,203,236,98,8,74.1,61.7,72.3,0.0,0.0
// mounts_9_3      c_war_boar_mount,War Boar,42324,Tactics +4.0,125.0,1,0,239,213,84,15,82.4,82.8,80.4,87.8,0.0
// mounts_9_4      c_raptor_aqua,Aqua Raptor,27027,Dex +6,125.0,1,0,184,500,106,7,70.0,60.0,65.0,50.0,0.0
// mounts_9_5      c_war_bear,War Bear,26662,Tactics +3.0,115.0,2,0,264,368,110,1,99.8,99.0,99.6,77.5,0.0
// mounts_9_6      c_bear_white,White Bear,8417,Armor +10,115.0,1,0,167,337,109,1,96.9,99.2,99.8,91.6,0.0
// mounts_9_7      c_beetle_fire,Fire Beetle,10268,Str +5,110.0,2,0689,219,312,96,116,82.4,98.5,97.2,0.0,42.0,"Usable backpack, max weight 2.500"
// mounts_9_8      c_boura_armoured,Armoured Boura,18168,Armor +20,125.0,1,0,308,499,70,15,89.6,89.3,66.8,0.0,0.0
// mounts_9_9      c_gryphon,Gryphon,26655,Archery +4.0,115.0,1,0,209,227,138,1,80.6,72.1,25.3,93.4,0.0
// mounts_9_10      c_gryphon_white,White Gryphon,26654,Archery +4.0,115.0,2,0,174,206,107,20,78.4,68.3,27.1,79.3,48.6
// mounts_9_11      c_hawkstrider,Hawkstrider,26669,Dex +4,100.0,1,0,163,210,109,1,65.2,32.1,19.0,23.3,0.0
// mounts_9_12      c_lion,Lion,40712,Dex +5,110.0,1,0,155,203,95,1,96.8,99.3,99.7,84.4,0.0
// mounts_9_13      c_panda,Panda Bear,26660,Str +5,110.0,1,0,164,203,94,1,95.6,98.7,99.6,69.3,0.0
// mounts_9_14      c_talbuk,Talbuk,26658,Dex +4,100.0,1,0,171,200,100,1,67.3,68.0,69.8,64.7,0.0
// mounts_9_15      c_tawny_windrider,Tawny Windrider,26657,Armor +20,115.0,1,0,167,205,104,1,98.1,99.1,99.9,81.9,0.0
// mounts_9_16      c_horse_undead,Undead Horse,27019,0,125.0,1,0,208,500,101,10,70.0,60.0,65.0,50.0,0.0
// mounts_9_17      c_wolf_black,Black Wolf,26656,Armor +20,115.0,1,0,189,205,91,1,99.2,98.9,99.9,66.0,0.0
// mounts_9_18      c_frost_wolf,Frost Wolf,27021,Tactics +3.0,125.0,1,0,178,340,97,8,70.0,60.0,65.0,50.0,0.0
// mounts_9_19      c_raptor_green,Green Raptor,27023,Dex +6,125.0,1,0,197,500,106,8,70.0,60.0,65.0,50.0,0.0
// mounts_9_20      c_raptor_purple,Purple Raptor,27025,Dex +4,125.0,1,0,196,500,96,9,70.0,60.0,65.0,50.0,0.0

// mounts_10_1      c_capybara_mount,Capybara,42363,0,90.0,1,0,135,203,57,8,50.4,52.3,19.4,0.0,0.0
// mounts_10_2      c_frost_mite,Frost Mite,40717,0,125.0,3,0,296,376,114,10,81.7,61.5,68.2,0.0,0.0
// mounts_10_3      c_camel,Camel,26659,Str +5,115.0,1,0,179,209,101,1,99.7,99.3,99.7,90.5,0.0
// mounts_10_4      c_camaleon,Chameleon,9753,0,120.0,2,0851,220,307,94,1,88.7,97.1,97.5,59.5,0.0
// mounts_10_5      c_drake_mount,Drake Mount,8406,Int +5,110.0,2,0,285,278,108,1,99.6,99.1,99.0,78.8,0.0

// mounts_11_1      c_coconut_crab_mount,Coconut Crab,41781,Armor +15,110.0,1,0,252,399,92,7,56.3,59.6,31.2,0.0,0.0
// mounts_11_2      c_cu_sidhe,Cu Sidhe,11670,Tactics +4.0,120.0,2,0,271,501,92,2,59.1,62.2,45.9,42.2,0.0
// mounts_11_3      c_skeletal_mount,Skeletal Mount,9751,Int +5,105.0,1,0,223,228,88,34,86.1,61.0,96.5,61.4,47.2
// mounts_11_4      c_skeletal_cat,Skeletal Cat,41272,0,115.1,2,0,196,30,83,6,88.2,77.4,29.2,0.0,0.0
// mounts_11_5      c_hellhound_ancient,Ancient Hellhound,16381,0,135.1,3,0,235,332,114,46,62.3,77.4,56.6,0.0,0.0
// mounts_11_6      c_tarantula,Tarantula,40406,0,115.1,3,0,155,323,82,9,84.1,73.7,19.5,0.0,0.0
// mounts_11_7      c_tiger_wild,Wild Tiger,38980,Dex +4,115.1,1,0,231,261,114,6,82.6,81.0,18.5,58.8,0.0

// mounts_12_1      c_dog_malamut,Alaskan Malamut Dog,42860,0,100.0,1,0,129,197,76,45,42.3,40.7,17.2,0.0,0.0
// mounts_12_2      c_dog_bernard,Bernard Dog,42862,0,100.0,1,0,132,199,80,39,48.5,50.0,18.8,0.0,0.0
// mounts_12_3      c_dog_terier,Black Terrier Dog,42863,0,100.0,1,0,150,187,78,46,41.1,40.8,14.5,0.0,0.0
// mounts_12_4      c_dog_dane,Great Dane Dog,42861,0,100.0,1,0,133,183,79,48,48.0,47.8,19.9,0.0,0.0
// mounts_12_5      c_dog_newfoundland,Newfoundland Dog,42859,0,100.0,1,0,150,185,83,37,46.7,49.0,10.5,0.0,0.0
// mounts_12_6      c_dog_rottweiler,Rottweiler Dog,42864,0,100.0,1,0,139,199,83,40,50.0,45.7,13.3,0.0,0.0

[function td]
src.dialogclose d_all_mounts
sdialog d_all_mounts

[DIALOG d_all_mounts]
150,50
PAGE 0
gumppic 100 100 510 0

if <isempty <src.ctag.mount_cat>>
    gumppictiled 127 138 180 212 1559
    dhtmlgump 136 114 160 20 0 0 <def.center><def.bfont_dbrown><def.bfont_size7><def.h3>Mount Catalogue
    button 456 100 512 512 1 0 1    // next page
    dorigin 336 100
    for 1 12
    button - *18 <def.dlg_btn_blue_1> 1 0 <dlocal._for>
    dhtmlgump +22 -3 80 20 0 0 <def.bfont_dbrown><def.bfont_size4><def.h3><strarg <def.mounts_<dlocal._for>>>
    endfor
else
    // <src.ctag0.mount_cat> > 1   // min category
    if <isempty <src.ctag.mount_id>>
        button 100 100 511 511 1 0 <eval <src.ctag0.mount_cat>-1>    // back page
        if <src.ctag0.mount_cat> < 12   // max category
            button 456 100 512 512 1 0 <eval <src.ctag0.mount_cat>+1>    // next page
        endif
        dhtmlgump 136 114 160 20 0 0 <def.center><def.bfont_dbrown><def.bfont_size7><def.h3><strarg <def.mounts_<src.ctag0.mount_cat>>>
        local.x=144
        local.y=138
        for 1 <streat <def.mounts_<src.ctag0.mount_cat>>>
        local.c ++
        args=<def.mounts_<src.ctag0.mount_cat>_<dlocal._for>>
        button <dlocal.x> <dlocal.y> <def.dlg_btn_green_1> 1 0 <eval <dlocal._for>+100>
        dhtmlgump <eval <dlocal.x>+22> <eval <dlocal.y>-3> 120 20 0 0 <def.bfont_dbrown><def.bfont_size4><argv[1]>
        local.y += 18
        if <dlocal.c>==11
            local.x += 190
            local.y=138
        endif
        endfor
    else
        button 100 100 511 511 1 0 <qval <src.ctag0.mount_id>==1?0:<eval <src.ctag0.mount_id>+99>>    // back page
        if <src.ctag0.mount_id> < <streat <def.mounts_<src.ctag0.mount_cat>>>   // max mount in category
            button 456 100 512 512 1 0 <eval <src.ctag0.mount_id>+101>    // next page
        endif
        args=<def.mounts_<src.ctag0.mount_cat>_<src.ctag0.mount_id>>
        dhtmlgump 136 192 160 20 0 0 <def.center><def.bfont_lblack><def.bfont_size7><def.h3><argv[1]>
        tilepic 212 150 <argv[2]> <argv[6]>  // npc icon, this coords will need some adjust
        dorigin 142 202
        dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Slots
        dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[5]>
        if <argv[6]>!=0
            dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Color
            dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[6]>
        endif
        dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Hits
        dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[8]>
        dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Strenght
        dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[7]>
        dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Dexterity
        dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[9]>
        dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Intelligence
        dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[10]>
        if <dargv[3]>!=0
            dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Bonus
            dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[3]>
        endif
        
        dorigin 332 100
        dhtmlgump - *18 150 20 0 0 <def.center><def.bfont_lblack><def.bfont_size7>Skills
        dhtmlgump - *20 80 20 0 0 <def.bfont_brown><def.bfont_size7>Taming
        dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[4]>
        dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Wrestling
        dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[11]>
        if <argv[12]> > 0
            dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Tactics
            dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[12]>
        endif
        if <argv[13]> > 0
            dhtmlgump - *17 140 20 0 0 <def.bfont_brown><def.bfont_size7>M. Resistance
            dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[13]>
        endif
        if <argv[14]> > 0
            dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Parrying
            dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[14]>
        endif
        if <argv[15]> > 0
            dhtmlgump - *17 80 20 0 0 <def.bfont_brown><def.bfont_size7>Magery
            dhtmlgump +70 - 80 20 0 0 <def.div_right><def.bfont_dbrown><def.bfont_size4><argv[15]>
        endif
        if !<isempty <argv[16]>>
            dhtmlgump - *20 150 20 0 0 <def.center><def.bfont_lblack><def.bfont_size7>Info
            dhtmlgump - *20 150 40 0 0 <def.center><def.bfont_brown><def.bfont_size1><argv[16]>.
        endif
        button - *50 <def.dlg_btn_blue_1> 1 0 5000
        dhtmlgump +22 -3 100 20 0 0 <def.bfont_lblack><def.bfont_size7>Create Mount
    endif
endif

[DIALOG d_all_mounts prebutton]
src.dialogclose d_all_mounts
if <argn1>!=0
    if <uid>!=<src> && <topobj>!=<src> || <isinbank>
        src.sysmessage @,,1 You can't reach <name>.
        src.ctag.mount_cat=
        src.ctag.mount_id=
        return 1
    endif
endif

[DIALOG d_all_mounts BUTTON]
ON=0
	if !<isempty <src.ctag.mount_id>>
		src.ctag.mount_id=
		dialog d_all_mounts
	elif !<isempty <src.ctag.mount_cat>>
		src.ctag.mount_cat=
		dialog d_all_mounts
	endif
	return 1

ON=1 99
    src.ctag.mount_cat=<argn1>
    dialog d_all_mounts

ON=101 199
    src.ctag.mount_id=<eval <argn1>-100>
    dialog d_all_mounts

on=5000
    args=<def.mounts_<src.ctag0.mount_cat>_<src.ctag0.mount_id>>
    serv.newnpc=<argv[0]>
    new.p=<src.p>
    new.f_makepetof <src>
    dialog d_all_mounts

[itemdef i_catalogue_mounts]
DEFNAME=i_catalogue_mounts
ID=i_book_lg
NAME=Mount Catalogue
TYPE=t_script
WEIGHT=2.0
VALUE=50
RESOURCES=98 i_parchment,4 i_hide
tag.itemlevel=0
SKILLMAKE=Inscription 100.0,AnimalLore 75.0,i_pen_and_ink
CATEGORY=Custom
SUBSECTION=Inscription Items
DESCRIPTION=Mount Catalogue
on=@create
    color=015e
on=@DClick
    src.ctag.mount_cat=
    src.ctag.mount_id=
    src.dialogclose d_all_mounts
    dialog d_all_mounts
    return 1

[EOF]